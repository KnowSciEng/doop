.decl FunctionInvResolvesTo(?ctx: configuration.Context, ?insn:FunctionInvocation_Insn, ?val:Value, ?hctx:configuration.HContext, ?fun:Function)


FunctionInvResolvesTo(?callerCtx, ?invo, ?value, ?hctx, ?tofunction),
configuration.ContextRequest(?callerCtx, ?hctx, ?invo, ?value, ?tofunction) :-
  VarPointsTo(?hctx, ?value, ?callerCtx, ?base),
  FunctionInvocation_Base(?invo, ?base),
  HeapAllocation_Type(?value, ?tofunction),
  isFunction(?tofunction).


isContext(?calleeCtx),
CallGraphEdge(?callerCtx, ?invocation, ?calleeCtx, ?tofunction) :-
  configuration.ContextRequest(?callerCtx, ?hctx, ?invocation, ?value, ?tofunction),
  configuration.ContextResponse(?callerCtx, ?hctx, ?invocation, ?value, ?calleeCtx).

.decl ResolvedActualParamValue(?callerCtx: configuration.Context, ?invo:Instruction, ?resolvedFunction:Function, ?paramName:symbol, ?hctx:configuration.HContext, ?paramValue:Value)

// Interprocedural Assign
VarPointsTo(?hctx, ?value, ?calleeCtx, ?formal),
ResolvedActualParamValue(?callerCtx, ?invo, ?tofunction, ?paramName, ?hctx, ?value):-
  CallGraphEdge(?callerCtx, ?invo, ?calleeCtx, ?tofunction),
  FunctionInvResolvesTo(?callerCtx, ?invo, ?baseValue, ?basehctx, ?tofunction),
  FormalParam(?paramIndex, ?paramName, ?tofunction, ?formal),
  (
    ActualPositionalParam(?paramIndex, ?invo, ?actual);
    ActualKeywordParam(_, ?invo, ?paramName, ?actual)
  ),
  VarPointsTo(?hctx, ?value, ?callerCtx, ?actual).

// Default arguments
VarPointsTo(?hctx, ?value, ?calleeCtx, ?formal),
ResolvedActualParamValue(?callerCtx, ?invo, ?tofunction, ?paramName, ?hctx, ?value):-
  CallGraphEdge(?callerCtx, ?invo, ?calleeCtx, ?tofunction),
  FunctionInvResolvesTo(?callerCtx, ?invo, ?baseValue, ?basehctx, ?tofunction),
  FormalParam(?paramIndex, ?paramName, ?tofunction, ?formal),
  !(
    ActualPositionalParam(?paramIndex, ?invo, _);
    ActualKeywordParam(_, ?invo, ?paramName, _)
  ),
  FormalParam_DefaultValFrom(?tofunction, ?paramName, ?default),
  VarPointsTo(?hctx,  ?value, _, ?default).       //TODO: REVIEW this at some point


//VarPointsTo(?hctx, ?value, ?callerCtx, ?return):-
//  CallGraphEdge(?callerCtx, ?invo, _, ?tofunction),
//  ReachableContext(?callerCtx, ?function),
//  Instruction_Function(?invo, ?function),
//  AssignInstruction_To(?invo, ?return),
//  Instruction_Function(?retInsn, ?tofunction),
//  !(isReturnInstruction(?retInsn)),
//  isNoneValue(?value),
//  isImmutableHContext(?hctx).

VarPointsTo(?hctx, ?value, ?callerCtx, ?return):-
  CallGraphEdge(?callerCtx, ?invo, _, ?tofunction),
  ReachableContext(?callerCtx, ?function),
  Instruction_Function(?invo, ?function),
  AssignInstruction_To(?invo, ?return),
  isReturnNone_Insn(?retInsn),
  Instruction_Function(?retInsn, ?tofunction),
  isNoneValue(?value),
  isImmutableHContext(?hctx).

VarPointsTo(?hctx, ?value, ?callerCtx, ?return):-
  CallGraphEdge(?callerCtx, ?invo, ?calleeCtx, ?tofunction),
  AssignInstruction_To(?invo, ?return),
  isReturnNonNone_Insn(?retInsn),
  Instruction_Function(?retInsn, ?tofunction),
  ReturnNonNone_Var(?retInsn, ?var),
  VarPointsTo(?hctx, ?value, ?calleeCtx, ?var).