//----------------------------------------------------------------------------------------------------------------------
//                                              MAYBE SOME BROADCASTING LOGIC
//----------------------------------------------------------------------------------------------------------------------




//----------------------------------------------------------------------------------------------------------------------
//                                              ADD(tf.add,+) also: See tf.add_n
//----------------------------------------------------------------------------------------------------------------------
.decl TfAddOrSubOp(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?addOrSubOp:Value, ?offset:number)
.output TfAddOrSubOp

.decl TfAddOrSubOpCannotBroadcast(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?addOrSubOp:Value)
.output TfAddOrSubOpCannotBroadcast

.decl TfAddOrSubOpBroadcastWarning(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?addOrSubOp:Value)
.output TfAddOrSubOpBroadcastWarning

.decl TfAddOrSubOpAllDimsHaveRes(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?addOrSubOp:Value)
.output TfAddOrSubOpAllDimsHaveRes

.decl TfAddOrSubOpAllDimsUpToXHaveRes(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?addOrSubOp:Value, ?index:number)
.output TfAddOrSubOpAllDimsUpToXHaveRes

.decl TfAddOrSubOpResultShapeListLength(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?addOrSubOp:Value, ?len:number)
.output TfAddOrSubOpResultShapeListLength

.decl TfAddOrSubOpResultShapeListContent(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?addOrSubOp:Value, ?index:number, ?indexVal:Value)
.output TfAddOrSubOpResultShapeListContent

TfAddOrSubOpResultShapeListLength(?ctx, ?insn, ?addOrSubOp, ?len1),
TensorOperation_NameToVal(?ctx, ?insn, ?addOrSubOp, "op1", ?tensor1Hctx, ?tensor1),
TensorOperation_NameToVal(?ctx, ?insn, ?addOrSubOp, "op2", ?tensor2Hctx, ?tensor2),
TfAddOrSubOp(?ctx, ?insn, ?addOrSubOp, ?offset):-
  isAssignBinop_Insn(?insn),
  AssignOper_From(?insn, ?x1, ?from1),
  AssignOper_From(?insn, ?x2, ?from2),
  ?x1 != ?x2,
  Assign_Operation(?insn, ?op),
  (?op = "add" ; ?op = "rem"),
  VarPointsTo(?tensor1Hctx, ?tensor1, ?ctx, ?from1),
  VarPointsTo(?tensor2Hctx, ?tensor2, ?ctx, ?from2),
  TensorLikeObjectShapeListLength(?tensor1Hctx, ?tensor1, ?len1),
  TensorLikeObjectShapeListLength(?tensor2Hctx, ?tensor2, ?len2),
  ?len1 >= ?len2,
  ?offset = ?len1 - ?len2,
  Instruction_Function(?insn, ?fun),
  Instruction_Index(?insn, ?index),
  ?addOrSubOp = PlaceHolderValueMacro(?op, ?fun, ?index, ?tensor1, ?tensor2).

TfAddOrSubOpResultShapeListContent(?ctx, ?insn, ?addOrSubOp, ?index, ?indexVal):-
  TfAddOrSubOp(?ctx, ?insn, ?addOrSubOp, ?offset),
  ?index < ?offset,
  TensorOperation_NameToVal(?ctx, ?insn, ?addOrSubOp, "op1", ?tensor1Hctx, ?tensor1),
  TensorLikeObjectShapeListContent(?tensor1Hctx, ?tensor1, ?index, ?indexVal).

TfAddOrSubOpResultShapeListContent(?ctx, ?insn, ?addOrSubOp, ?index, ?indexVal):-
  TfAddOrSubOp(?ctx, ?insn, ?addOrSubOp, ?offset),
  ?index >= ?offset,
  TensorOperation_NameToVal(?ctx, ?insn, ?addOrSubOp, "op1", ?tensor1Hctx, ?tensor1),
  TensorOperation_NameToVal(?ctx, ?insn, ?addOrSubOp, "op2", ?tensor2Hctx, ?tensor2),
  TensorLikeObjectShapeListContent(?tensor1Hctx, ?tensor1, ?index, ?indexVal),
  TensorLikeObjectShapeListContent(?tensor2Hctx, ?tensor2, ?index - ?offset, ?indexVal).

TfAddOrSubOpBroadcastWarning(?ctx, ?insn, ?addOrSubOp),
TfAddOrSubOpResultShapeListContent(?ctx, ?insn, ?addOrSubOp, ?index, ?indexVal):-
  TfAddOrSubOp(?ctx, ?insn, ?addOrSubOp, ?offset),
  ?index >= ?offset,
  TensorOperation_NameToVal(?ctx, ?insn, ?addOrSubOp, "op1", ?tensor1Hctx, ?tensor1),
  TensorOperation_NameToVal(?ctx, ?insn, ?addOrSubOp, "op2", ?tensor2Hctx, ?tensor2),
  TensorLikeObjectShapeListContent(?tensor1Hctx, ?tensor1, ?index, ?indexVal1),
  TensorLikeObjectShapeListContent(?tensor2Hctx, ?tensor2, ?index - ?offset, ?indexVal2),
  ?indexVal1 != ?indexVal2,
  (
  (?indexVal1 = "<num-constant(int):1>", ?indexVal = ?indexVal2)
  ;
  (?indexVal2 = "<num-constant(int):1>", ?indexVal = ?indexVal2)
  ).

TfAddOrSubOpCannotBroadcast(?ctx, ?insn, ?addOrSubOp):-
  TfAddOrSubOp(?ctx, ?insn, ?addOrSubOp, ?offset),
  ?index >= ?offset,
  TensorOperation_NameToVal(?ctx, ?insn, ?addOrSubOp, "op1", ?tensor1Hctx, ?tensor1),
  TensorOperation_NameToVal(?ctx, ?insn, ?addOrSubOp, "op2", ?tensor2Hctx, ?tensor2),
  TensorLikeObjectShapeListContent(?tensor1Hctx, ?tensor1, ?index, ?indexVal1),
  TensorLikeObjectShapeListContent(?tensor2Hctx, ?tensor2, ?index - ?offset, ?indexVal2),
  ?indexVal1 != ?indexVal2,
  ?indexVal1 != "<num-constant(int):1>",
  ?indexVal2 != "<num-constant(int):1>".

TfAddOrSubOpAllDimsUpToXHaveRes(?ctx, ?insn, ?addOrSubOp, 0):-
  TfAddOrSubOpResultShapeListContent(?ctx, ?insn, ?addOrSubOp, 0, _).

TfAddOrSubOpAllDimsUpToXHaveRes(?ctx, ?insn, ?addOrSubOp, ?index):-
  ?index > 0,
  TfAddOrSubOpResultShapeListContent(?ctx, ?insn, ?addOrSubOp, ?index, _),
  TfAddOrSubOpAllDimsUpToXHaveRes(?ctx, ?insn, ?addOrSubOp, ?index - 1).

TensorOperationProducesOutput(?ctx, ?insn, ?addOrSubOp):-
  TfAddOrSubOpAllDimsHaveRes(?ctx, ?insn, ?addOrSubOp).

TfAddOrSubOpAllDimsHaveRes(?ctx, ?insn, ?addOrSubOp):-
  TfAddOrSubOpResultShapeListLength(?ctx, ?insn, ?addOrSubOp, ?len),
  TfAddOrSubOpAllDimsUpToXHaveRes(?ctx, ?insn, ?addOrSubOp, ?len-1).

TensorShapeListLength(?shapeHctx, ?tensorShapeVal, ?len):-
  TfAddOrSubOpResultShapeListLength(?ctx, ?insn, ?addOrSubOp, ?len),
  TensorOperationProducesOutput(?ctx, ?insn, ?addOrSubOp),
  AssignInstruction_To(?insn, ?var),
  VarPointsTo(?hctx, ?addOrSubOp, ?ctx, ?var),
  TensorShape(?hctx, ?addOrSubOp, ?shapeHctx, ?tensorShapeVal).

TensorShapeListContent(?shapeHctx, ?tensorShapeVal, ?index, ?indexVal):-
  TfAddOrSubOp(?ctx, ?insn, ?addOrSubOp, _),
  TensorOperationProducesOutput(?ctx, ?insn, ?addOrSubOp),
  AssignInstruction_To(?insn, ?var),
  VarPointsTo(?hctx, ?addOrSubOp, ?ctx, ?var),
  TensorShape(?hctx, ?addOrSubOp, ?shapeHctx, ?tensorShapeVal),
  TfAddOrSubOpResultShapeListContent(?ctx, ?insn, ?addOrSubOp, ?index, ?indexVal).

//----------------------------------------------------------------------------------------------------------------------
//                                              MATMUL
//----------------------------------------------------------------------------------------------------------------------
.decl TfMatMulOp(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?matMulOp:Value)
.output TfMatMulOp


//----------------------------------------------------------------------------------------------------------------------
//                                              MULTIPLY
//----------------------------------------------------------------------------------------------------------------------



//----------------------------------------------------------------------------------------------------------------------
//                                              * OP(overloaded)
//----------------------------------------------------------------------------------------------------------------------



//----------------------------------------------------------------------------------------------------------------------
//                                               TENSORDOT
//----------------------------------------------------------------------------------------------------------------------

