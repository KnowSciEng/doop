.decl EntryPoint(?method:Method)
.decl PossibleEntryPoint(?method:Method)
.decl ClassHasPossibleEntryPoint(?class:ReferenceType)

ClassHasPossibleEntryPoint(?class),
PossibleEntryPoint(?method) :-
  Method_DeclaringType(?method, ?class),
  Method_Modifier("public", ?method),
//// Remove comment for original behavior
//   !Method_Modifier("static", ?method),
  !ClassModifier("private", ?class).

// Find highest level in the naming hierarchy that contains public
// methods.
.decl PrefixAbove(class:ReferenceType, str:symbol, ?len:number)
.decl PrefixBelow(class:ReferenceType, str:symbol)

PrefixAbove(?classAbove, substr(?classAbove, 0, ?lastDotPos+1), ?lastDotPos+1) :-
  ClassHasPossibleEntryPoint(?classAbove),
  LastDot(?classAbove, ?lastDotPos).

PrefixBelow(?classBelow, substr(?classBelow, 0, ?lastDotPos)) :-
  ClassHasPossibleEntryPoint(?classBelow),
  LastDot(?classBelow, ?lastDotPos).

.decl ClassIsAboveOtherInNamingHierarchy(?classAbove:ReferenceType, ?classBelow:ReferenceType)

ClassIsAboveOtherInNamingHierarchy(?classAbove, ?classBelow) :-
  // One's prefix before "." is a proper prefix of the other's prefix before "."
  PrefixAbove(?classAbove, ?prefixAbove, ?prefixLen),
  PrefixBelow(?classBelow, ?prefixBelow),
  substr(?prefixBelow, 0, ?prefixLen) = ?prefixAbove.

.decl ClassWithTopLevelEntryPoint(?class:ReferenceType)
.decl TopLevelApplicationClass(?class:ReferenceType)
.decl ClassToConsider(?class:ReferenceType)
.decl TopLevelEntryPoint(?method:Method)

ClassWithTopLevelEntryPoint(?class) :-
  ClassHasPossibleEntryPoint(?class),
  !ClassIsAboveOtherInNamingHierarchy(_, ?class).

TopLevelApplicationClass(?class) :-
  ClassWithTopLevelEntryPoint(?class),
  ApplicationClass(?class).

ClassToConsider(?class) :-
  TopLevelApplicationClass(?class).

ClassToConsider(?class) :-
  ApplicationClass(?class),
  !TopLevelApplicationClass(_).

TopLevelEntryPoint(?method) :-
  PossibleEntryPoint(?method),
  Method_DeclaringType(?method, ?class),
  ClassToConsider(?class).

#ifdef OPEN_PROGRAMS
// The original definition that Neville had
EntryPoint(?method) :-
  PossibleEntryPoint(?method),
  Method_DeclaringType(?method, ?class),
  substr(?class, 0, 4) = "java".
#else
// The definition we currently want as default
EntryPoint(?method) :-
  PossibleEntryPoint(?method).
//  TopLevelEntryPoint(?method).
#endif
