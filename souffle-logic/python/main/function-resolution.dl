.decl FunctionInvResolvesTo(?ctx: configuration.Context, ?insn:FunctionInvocation_Insn, ?val:Value, ?hctx:configuration.HContext, ?fun:Function)


FunctionInvResolvesTo(?callerCtx, ?invo, ?value, ?hctx, ?tofunction),
configuration.ContextRequest(?callerCtx, ?hctx, ?invo, ?value, ?tofunction) :-
  Reachable(?fun),
  ReachableContext(?callerCtx, ?fun),
  Instruction_Function(?invo, ?fun),
  FunctionInvocation_Base(?invo, ?base),
  VarPointsTo(?hctx, ?value, ?callerCtx, ?base),
  HeapAllocation_Type(?value, ?tofunction),
  isFunction(?tofunction).


isContext(?calleeCtx),
CallGraphEdge(?callerCtx, ?invocation, ?calleeCtx, ?tofunction) :-
  configuration.ContextRequest(?callerCtx, ?hctx, ?invocation, ?value, ?tofunction),
  configuration.ContextResponse(?callerCtx, ?hctx, ?invocation, ?value, ?calleeCtx).

.decl ResolvedActualParamValue(?callerCtx: configuration.Context, ?invo:Instruction, ?baseValue:Value, ?basehctx:configuration.HContext, ?paramIndex:number, ?paramName:symbol, ?paramValue:Value, ?hctx:configuration.HContext)

// Interprocedural Assign
VarPointsTo(?hctx, ?value, ?calleeCtx, ?formal),
ResolvedActualParamValue(?callerCtx, ?invo, ?baseValue, ?basehctx, ?paramIndex, ?paramName, ?value, ?hctx):-
  CallGraphEdge(?callerCtx, ?invo, ?calleeCtx, ?tofunction),
  FunctionInvResolvesTo(?callerCtx, ?invo, ?baseValue, ?basehctx, ?tofunction),
  FormalParam(?paramIndex, ?paramName, ?function, ?formal),
  (
    ActualPositionalParam(?paramIndex, ?invo, ?actual);
    ActualKeywordParam(_, ?invo, ?paramName, ?actual)
  ),
  VarPointsTo(?hctx, ?value, ?callerCtx, ?formal).

// Default arguments
VarPointsTo(?hctx, ?value, ?calleeCtx, ?formal),
ResolvedActualParamValue(?callerCtx, ?invo, ?baseValue, ?basehctx, ?paramIndex, ?paramName, ?value, ?hctx):-
  CallGraphEdge(?callerCtx, ?invo, ?calleeCtx, ?tofunction),
  FunctionInvResolvesTo(?calleCtx, ?invo, ?baseValue, ?basehctx, ?tofunction),
  FormalParam(?paramIndex, ?paramName, ?function, ?formal),
  !(
    ActualPositionalParam(?paramIndex, ?invo, _);
    ActualKeywordParam(_, ?invo, ?paramName, _)
  ),
  FormalParam_DefaultValFrom(?function, ?paramName, ?formal),
  VarPointsTo(?hctx,  ?value, ?callerCtx, ?formal).