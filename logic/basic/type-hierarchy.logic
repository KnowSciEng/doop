/**
 * The following rules check whether type S can be cast to type T. Implicitly,
 * they expose the type hierarchy.
 */
SupertypeOf(?s, ?t) <- SubtypeOf(?t, ?s).


/**
 * If S is an ordinary (nonarray) class, then
 * - S must be the same class as T
 * - or a subclass of T
 */
SubtypeOf(?s, ?s) <-
   ClassType(?s).

SubtypeOf(?s, ?t) <-
   Subclass(?t, ?s).

/**
 * If T is an interface type, then
 * - S must implement interface T
 */
SubtypeOf(?s, ?t) <-
   ClassType(?s),
   Superinterface(?t, ?s).

/**
 * If S is an interface type, then
 * - If T is a class type, then T must be Object
 */
SubtypeOf(?s, ?t) <-
   InterfaceType(?s),
   Type:Id(?t:"java.lang.Object").

/**
 * If T is an interface type, then
 * - T must be the same interface as S
 * - or a superinterface of S
 */
SubtypeOf(?s, ?s) <-
   InterfaceType(?s).

SubtypeOf(?s, ?t) <-
   InterfaceType(?s),
   Superinterface(?t, ?s).

/**
 *  If S is a class representing the array type SC[], that is, an array of
 *  components of type SC, then
 *  - If T is a class type, then T must be Object
 */
SubtypeOf(?s, ?t) <-
   ArrayType(?s),
   Type:Id(?t:"java.lang.Object").

/**
 *  If T is an array type TC[], that is, an array of components of type TC,
 *  then one of the following must be true
 *  - TC and SC are the same primitive type
 */
SubtypeOf(?s, ?t) <-
   ArrayType(?s),
   ArrayType(?t),
   ComponentType[?s] = ?sc,
   ComponentType[?t] = ?sc,
   PrimitiveType(?sc).



/**
 * - TC and SC are reference types (2.4.6), and type SC can be cast to TC by
 * recursive application of these rules
 */
SubtypeOf(?s, ?t) <-
   ComponentType[?s] = ?sc,
   ComponentType[?t] = ?tc,
   ReferenceType(?sc),
   ReferenceType(?tc),
   SubtypeOf(?sc, ?tc).

/**
 *  If T is an interface type, T must be one of the interfaces implemented by
 *  arrays (2.15)
 */
SubtypeOf(?s, ?t) <-
   ArrayType(?s),
   InterfaceType(?t),
   Type:Id(?t:"java.lang.Cloneable").

SubtypeOf(?s, ?t) <-
   ArrayType(?s),
   InterfaceType(?t),
   Type:Id(?t:"java.io.Serializable").


/**
 *  Null type acts as a bottom type
 */
SubtypeOf(?s, ?t) <-
   Type:null[] = ?s,
   ReferenceType(?t),
   ?t != ?s.


/**
 * Subtyping among Primitive Types
 */
SubtypeOf(Type:float[], Type:double[]).
SubtypeOf(Type:long[], Type:float[]).
SubtypeOf(Type:int[], Type:long[]).
SubtypeOf(Type:char[], Type:int[]).
SubtypeOf(Type:short[], Type:int[]).
SubtypeOf(Type:byte[], Type:short[]).

/**
 * Each type is a subtype / supertype of itself
 */
SubtypeOf(?t, ?t)
   <- Type(?t).


// Extra logic for determining whether it is possible to subclass a type (based on Oracle Souffle)
Unsubclassable(?type) -> Type(?type).

Unsubclassable("java.lang.ClassLoader").
Unsubclassable("java.lang.SecurityManager").


//
// Subclassable
//
Subclassable(?type) -> Type(?type).

Subclassable(?type) <-
  ClassModifier("public", ?type),
  ! ClassModifier("final", ?type),
  ClassConstructor(?method, ?type),
  Method:Modifier("public", ?method),
  ! Unsubclassable(?type).

Subclassable(?type) <-
  ClassModifier("public", ?type),
  ! ClassModifier("final", ?type),
  ClassConstructor(?method, ?type),
  Method:Modifier("protected", ?method),
  ! Unsubclassable(?type).

