//----------------------------------------------------------------------------------------------------------------------
//                                              RESHAPE OP
//----------------------------------------------------------------------------------------------------------------------

.decl ReshapeOperation(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?reshapeOpVal:Value)
.output ReshapeOperation

.decl ReshapeOperationInvalid(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?reshapeOpVal:Value)
.output ReshapeOperationInvalid

.decl ReshapeOperationPossibleMissalignment(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?reshapeOpVal:Value)
.output ReshapeOperationPossibleMissalignment

.decl ReshapeOperationBatchContamination(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?reshapeOpVal:Value)
.output ReshapeOperationBatchContamination

.decl ReshapeOperationCorrectTypeArgs(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?reshapeOpVal:Value)
.output ReshapeOperationCorrectTypeArgs

.decl ReshapeOperationTensorAndListSameMult(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?reshapeOpVal:Value)
.output ReshapeOperationTensorAndListSameMult

.decl ReshapeOperationTensorAndListDiffMult(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?reshapeOpVal:Value)
.output ReshapeOperationTensorAndListDiffMult

.decl ReshapeOperationReplaceMinus1WithVal(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?reshapeOpVal:Value, ?newVal:Value)
.output ReshapeOperationReplaceMinus1WithVal

#define ReshapeValueMacro(op,fun, index, val1, val2) cat("<tensor ", cat(fun, cat("/", cat(op, cat( to_string(index), cat("/", cat(val1, cat("+", cat(val2, ">")))))))))

//Checks if the required args are given and their values have correct values


TensorOperation_GetDTypeFromName(?ctx, ?insn, ?reshapeOpVal, "tensor"),
TensorOperation_NameToVal(?ctx, ?insn, ?reshapeOpVal, "tensor", ?tensorHctx, ?tensorArgVal),
TensorOperation_NameToVal(?ctx, ?insn, ?reshapeOpVal, "shape", ?dimListHctx, ?dimListArgVal),
ReshapeOperation(?ctx, ?insn, ?reshapeOpVal):-
  FunctionInvResolvesTo(?ctx, ?insn, _, _, "<tensorflow.reshape>"),
  ResolvedActualParamValue(?ctx, ?insn, "<tensorflow.reshape>", "tensor", ?tensorHctx, ?tensorArgVal),
  ResolvedActualParamValue(?ctx, ?insn, "<tensorflow.reshape>", "shape", ?dimListHctx, ?dimListArgVal),
  Instruction_Function(?insn, ?fun),
  Instruction_Index(?insn, ?index),
  ?reshapeOpVal = ReshapeValueMacro("reshape", ?fun, ?index, ?tensorArgVal, ?dimListArgVal).


ReshapeOperationCorrectTypeArgs(?ctx, ?insn, ?reshapeOpVal):-
  ReshapeOperation(?ctx, ?insn, ?reshapeOpVal),
  TensorOperation_NameToVal(?ctx, ?insn, ?reshapeOpVal, "tensor", _, ?tensorArgVal),
  Value_Type(?tensorArgVal, "<tensorflow.Tensor>"),
  TensorOperation_NameToVal(?ctx, ?insn, ?reshapeOpVal, "shape", ?dimListHctx, ?dimListArgVal),
  ListOfIntConsts(?dimListHctx, ?dimListArgVal).
  //PyConstListSpecialValueCount(?dimListArgVal, "<num-constant(int):-1>", ?specialValueCount),
  //?specialValueCount < 2.


// Checks if, for the two list arguments --the shape list of the tensor value, and the reshape shape list--
// the multiplication of all the elements(excluding -1 and None) has the same results
ReshapeOperationTensorAndListSameMult(?ctx, ?insn, ?reshapeOpVal):-
  ReshapeOperationCorrectTypeArgs(?ctx, ?insn, ?reshapeOpVal),
  TensorOperation_NameToVal(?ctx, ?insn, ?reshapeOpVal, "tensor", ?tensorHctx, ?tensorArgVal),
  TensorShape(?tensorHctx, ?tensorArgVal, ?shapeHctx, ?tensorShapeVal),
  ConstListMultAll(?shapeHctx, ?tensorShapeVal,?tensorRes),
  TensorOperation_NameToVal(?ctx, ?insn, ?reshapeOpVal, "shape", ?dimListHctx, ?dimListArgVal),
  ConstListMultAll(?dimListHctx, ?dimListArgVal,?dimListRes),
  ?tensorRes = ?dimListRes.

// The opposite of the previous rule
ReshapeOperationTensorAndListDiffMult(?ctx, ?insn, ?reshapeOpVal):-
  ReshapeOperationCorrectTypeArgs(?ctx, ?insn, ?reshapeOpVal),
  TensorOperation_NameToVal(?ctx, ?insn, ?reshapeOpVal, "tensor", ?tensorHctx, ?tensorArgVal),
  TensorShape(?tensorHctx, ?tensorArgVal, ?shapeHctx, ?tensorShapeVal),
  ConstListMultAll(?shapeHctx, ?tensorShapeVal,?tensorRes),
  TensorOperation_NameToVal(?ctx, ?insn, ?reshapeOpVal, "shape", ?dimListHctx, ?dimListArgVal),
  ConstListMultAll(?dimListHctx, ?dimListArgVal,?dimListRes),
  ?tensorRes != ?dimListRes.

//All the input tensors dims are known
//The shape of the dimension list does not contain -1
//isValidTensorReshapeInvocationClass2(?insn, ?tensorArgVal, ?dimListArgVal),,
TensorOperationProducesOutput(?ctx, ?insn, ?reshapeOpVal):-
  ReshapeOperationTensorAndListSameMult(?ctx, ?insn, ?reshapeOpVal),
  TensorOperation_NameToVal(?ctx, ?insn, ?reshapeOpVal, "tensor", ?tensorHctx, ?tensorArgVal),
  TensorObjectHasConcreteDimensions(?tensorHctx, ?tensorArgVal),
  TensorOperation_NameToVal(?ctx, ?insn, ?reshapeOpVal, "shape", ?dimListHctx, ?dimListArgVal),
  ListOfPosIntConsts(?dimListHctx, ?dimListArgVal).

//All the input tensors dims are known
//The shape of the dimension list contains -1  but it is 1
ReshapeOperationReplaceMinus1WithVal(?ctx, ?insn, ?reshapeOpVal, "<num-constant(int):1>"),
TensorOperationProducesOutput(?ctx, ?insn, ?reshapeOpVal):-
  ReshapeOperationTensorAndListSameMult(?ctx, ?insn, ?reshapeOpVal),
  TensorOperation_NameToVal(?ctx, ?insn, ?reshapeOpVal, "tensor", ?tensorHctx, ?tensorArgVal),
  TensorObjectHasConcreteDimensions(?tensorHctx, ?tensorArgVal),
  TensorOperation_NameToVal(?ctx, ?insn, ?reshapeOpVal, "shape", ?dimListHctx, ?dimListArgVal),
  ListOfIntConstsAtIndexPointsTo(?dimListHctx, ?dimListArgVal, _, "<num-constant(int):-1>").

//All the input tensors dims are known
//MAYBE GENERALISE THIS TO COVER THE ABOVE CASE AS WELL
isIntConstant(?replaceNumStr),
Value_Num(?replaceNumVal, ?replaceNumStr),
ReshapeOperationReplaceMinus1WithVal(?ctx, ?insn, ?reshapeOpVal, ?replaceNumVal),
TensorOperationProducesOutput(?ctx, ?insn, ?reshapeOpVal):-
  ReshapeOperationCorrectTypeArgs(?ctx, ?insn, ?reshapeOpVal),
  ReshapeOperationTensorAndListDiffMult(?ctx, ?insn, ?reshapeOpVal),
  TensorOperation_NameToVal(?ctx, ?insn, ?reshapeOpVal, "shape", ?dimListHctx, ?dimListArgVal),
  ListOfIntConstsAtIndexPointsTo(?dimListHctx, ?dimListArgVal, _, "<num-constant(int):-1>"),
  TensorOperation_NameToVal(?ctx, ?insn, ?reshapeOpVal, "tensor", ?tensorHctx, ?tensorArgVal),
  TensorObjectHasConcreteDimensions(?tensorHctx, ?tensorArgVal),
  TensorShape(?tensorHctx, ?tensorArgVal, ?shapeHctx, ?tensorShapeVal),
  ConstListMultAll(?shapeHctx, ?tensorShapeVal,?tensorMultRes),
  ConstListMultAll(?dimListHctx, ?dimListArgVal,?dimListMultRes),
  ?tensorMultRes % ?dimListMultRes = 0,
  ?replaceNumStr = to_string(?tensorMultRes / ?dimListMultRes),
  ?replaceNumVal = cat("<num-constant(int):",cat(?replaceNumStr,">")).

ReshapeOperationInvalid(?ctx, ?insn, ?reshapeOpVal):-
  ReshapeOperationCorrectTypeArgs(?ctx, ?insn, ?reshapeOpVal),
  ReshapeOperationTensorAndListDiffMult(?ctx, ?insn, ?reshapeOpVal),
  TensorOperation_NameToVal(?ctx, ?insn, ?reshapeOpVal, "shape", ?dimListHctx, ?dimListArgVal),
  ListOfIntConstsAtIndexPointsTo(?dimListHctx, ?dimListArgVal, _, "<num-constant(int):-1>"),
  TensorOperation_NameToVal(?ctx, ?insn, ?reshapeOpVal, "tensor", ?tensorHctx, ?tensorArgVal),
  TensorObjectHasConcreteDimensions(?tensorHctx, ?tensorArgVal),
  TensorShape(?tensorHctx, ?tensorArgVal, ?shapeHctx, ?tensorShapeVal),
  ConstListMultAll(?shapeHctx, ?tensorShapeVal,?tensorMultRes),
  ConstListMultAll(?dimListHctx, ?dimListArgVal,?dimListMultRes),
  ?tensorMultRes % ?dimListMultRes != 0.

//RECHECK THIS!
ReshapeOperationInvalid(?ctx, ?insn, ?reshapeOpVal):-
  ReshapeOperationCorrectTypeArgs(?ctx, ?insn, ?reshapeOpVal),
  ReshapeOperationTensorAndListDiffMult(?ctx, ?insn, ?reshapeOpVal),
  TensorOperation_NameToVal(?ctx, ?insn, ?reshapeOpVal, "shape", ?dimListHctx, ?dimListArgVal),
  ListOfPosIntConsts(?dimListHctx, ?dimListArgVal),
  TensorOperation_NameToVal(?ctx, ?insn, ?reshapeOpVal, "tensor", ?tensorHctx, ?tensorArgVal),
  TensorObjectHasConcreteDimensions(?tensorHctx, ?tensorArgVal).


ReshapeOperationReplaceMinus1WithVal(?ctx, ?insn, ?reshapeOpVal, "<None>"),
TensorOperationProducesOutput(?ctx, ?insn, ?reshapeOpVal):-
  ReshapeOperationTensorAndListSameMult(?ctx, ?insn, ?reshapeOpVal),
  TensorOperation_NameToVal(?ctx, ?insn, ?reshapeOpVal, "shape", ?dimListHctx, ?dimListArgVal),
  TensorOperation_NameToVal(?ctx, ?insn, ?reshapeOpVal, "tensor", ?tensorHctx, ?tensorArgVal),
  TensorShape(?tensorHctx, ?tensorArgVal, ?shapeHctx, ?tensorShapeVal),
  ListOfIntConstsAtIndexPointsTo(?dimListHctx, ?dimListArgVal, ?pos, "<num-constant(int):-1>"),
  ListOfConstsAtIndexPointsTo(?shapeHctx, ?tensorShapeVal, ?pos, "<None>").

ReshapeOperationBatchContamination(?ctx, ?insn, ?reshapeOpVal),
ReshapeOperationReplaceMinus1WithVal(?ctx, ?insn, ?reshapeOpVal, "<None>"),
TensorOperationProducesOutput(?ctx, ?insn, ?reshapeOpVal):-
  ReshapeOperationTensorAndListDiffMult(?ctx, ?insn, ?reshapeOpVal),
  TensorOperation_NameToVal(?ctx, ?insn, ?reshapeOpVal, "shape", ?dimListHctx, ?dimListArgVal),
  TensorOperation_NameToVal(?ctx, ?insn, ?reshapeOpVal, "tensor", ?tensorHctx, ?tensorArgVal),
  TensorShape(?tensorHctx, ?tensorArgVal, ?shapeHctx, ?tensorShapeVal),
  ListOfIntConstsAtIndexPointsTo(?dimListHctx, ?dimListArgVal, _, "<num-constant(int):-1>"),
  ListOfConstsAtIndexPointsTo(?shapeHctx, ?tensorShapeVal, _, "<None>").

ReshapeOperationReplaceMinus1WithVal(?ctx, ?insn, ?reshapeOpVal, "<None>"),
ReshapeOperationPossibleMissalignment(?ctx, ?insn, ?reshapeOpVal),
TensorOperationProducesOutput(?ctx, ?insn, ?reshapeOpVal):-
  ReshapeOperationTensorAndListSameMult(?ctx, ?insn, ?reshapeOpVal),
  TensorOperation_NameToVal(?ctx, ?insn, ?reshapeOpVal, "shape", ?dimListHctx, ?dimListArgVal),
  TensorOperation_NameToVal(?ctx, ?insn, ?reshapeOpVal, "tensor", ?tensorHctx, ?tensorArgVal),
  TensorShape(?tensorHctx, ?tensorArgVal, ?shapeHctx, ?tensorShapeVal),
  ListOfIntConstsAtIndexPointsTo(?dimListHctx, ?dimListArgVal, ?pos1, "<num-constant(int):-1>"),
  ListOfConstsAtIndexPointsTo(?shapeHctx, ?tensorShapeVal, ?pos2, "<None>"),
  ?pos1 != ?pos2.

TensorShapeListLength(?shapeHctx, ?tensorShapeVal, ?size),
TensorShapeListContent(?shapeHctx, ?tensorShapeVal, ?index, ?indexValue):-
  ReshapeOperation(?ctx, ?insn, ?reshapeOpVal),
  TensorOperationProducesOutput(?ctx, ?insn, ?reshapeOpVal),
  TensorOperation_NameToVal(?ctx, ?insn, ?reshapeOpVal, "shape", ?dimListHctx, ?dimListArgVal),
  AssignInstruction_To(?insn, ?var),
  VarPointsTo(?hctx, ?reshapeOpVal, ?ctx, ?var),
  TensorShape(?hctx, ?reshapeOpVal, ?shapeHctx, ?tensorShapeVal),
  ListInitialLength(?dimListArgVal, ?size),
  ListOfConstsAtIndexPointsTo(?dimListHctx, ?dimListArgVal, ?index, ?indexValue),
  ?indexValue != "<num-constant(int):-1>".

TensorShapeListLength(?shapeHctx, ?tensorShapeVal, ?size),
TensorShapeListContent(?shapeHctx, ?tensorShapeVal, ?index, ?indexValue):-
  ReshapeOperation(?ctx, ?insn, ?reshapeOpVal),
  TensorOperationProducesOutput(?ctx, ?insn, ?reshapeOpVal),
  TensorOperation_NameToVal(?ctx, ?insn, ?reshapeOpVal, "shape", ?dimListHctx, ?dimListArgVal),
  AssignInstruction_To(?insn, ?var),
  VarPointsTo(?hctx, ?reshapeOpVal, ?ctx, ?var),
  TensorShape(?hctx, ?reshapeOpVal, ?shapeHctx, ?tensorShapeVal),
  ListInitialLength(?dimListArgVal, ?size),
  ListOfConstsAtIndexPointsTo(?dimListHctx, ?dimListArgVal, ?index, "<num-constant(int):-1>"),
  ReshapeOperationReplaceMinus1WithVal(?ctx, ?insn, ?reshapeOpVal, ?indexValue).


//----------------------------------------------------------------------------------------------------------------------
//                                              SQUEEZE OP
//----------------------------------------------------------------------------------------------------------------------




//----------------------------------------------------------------------------------------------------------------------
//                                              EXPAND DIMS OP
//----------------------------------------------------------------------------------------------------------------------

.decl ExpandDimsOp(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?expDimsVal:Value)
.output ExpandDimsOp

.decl ExpandDimsOpInTensorShapeLength(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?expDimsVal:Value, ?length:number)
.output ExpandDimsOpInTensorShapeLength

.decl ExpandDimsOpAxisNum(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?expDimsVal:Value, ?axis:number)
.output ExpandDimsOpAxisNum

.decl ExpandDimsValidAxisNum(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?expDimsVal:Value)
.output ExpandDimsValidAxisNum

.decl ExpandDimsInvalidAxisNum(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?expDimsVal:Value)
.output ExpandDimsInvalidAxisNum

.decl ExpandDimsOpDim(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?expDimsVal:Value, ?dim:number)
.output ExpandDimsOpDim


TensorOperation_GetDTypeFromName(?ctx, ?insn, ?expDimsVal, "tensor"),
TensorOperation_NameToVal(?ctx, ?insn, ?expDimsVal, "input", ?tensorHctx, ?tensorArgVal),
TensorOperation_NameToVal(?ctx, ?insn, ?expDimsVal, "axis", ?axisHct, ?axis),
ExpandDimsOp(?ctx, ?insn, ?expDimsVal):-
  FunctionInvResolvesTo(?ctx, ?insn, _, _, "<tensorflow.expand_dims>"),
  ResolvedActualParamValue(?ctx, ?insn, "<tensorflow.expand_dims>", "input", ?tensorHctx, ?tensorArgVal),
  ResolvedActualParamValue(?ctx, ?insn, "<tensorflow.expand_dims>", "axis", ?axisHct, ?axis),
  Instruction_Function(?insn, ?fun),
  Instruction_Index(?insn, ?index),
  ?expDimsVal = ReshapeValueMacro("expandDims", ?fun, ?index, ?tensorArgVal, ?axis).

ExpandDimsOpAxisNum(?ctx, ?insn, ?expDimsVal, ?axisNum):-
  ExpandDimsOp(?ctx, ?insn, ?expDimsVal),
  ResolvedActualParamValue(?ctx, ?insn, "<tensorflow.expand_dims>", "axis", _, ?axis),
  Value_Num(?axis, ?axisNumStr),
  ?axisNum = to_number(?axisNumStr).

ExpandDimsOpInTensorShapeLength(?ctx, ?insn, ?expDimsVal, ?len):-
  ExpandDimsOp(?ctx, ?insn, ?expDimsVal),
  TensorOperation_NameToVal(?ctx, ?insn, ?expDimsVal, "input", ?tensorHctx, ?tensorArgVal),
  TensorLikeObjectShapeListLength(?tensorHctx, ?tensorArgVal, ?len).

ExpandDimsValidAxisNum(?ctx, ?insn, ?expDimsVal):-
  ExpandDimsOpAxisNum(?ctx, ?insn, ?expDimsVal, ?axisNum),
  ExpandDimsOpInTensorShapeLength(?ctx, ?insn, ?expDimsVal, ?len),
  ?axisNum <= ?len,
  - ?len - 1 <= ?axisNum.

ExpandDimsInvalidAxisNum(?ctx, ?insn, ?expDimsVal):-
  ExpandDimsOpAxisNum(?ctx, ?insn, ?expDimsVal, ?axisNum),
  ExpandDimsOpInTensorShapeLength(?ctx, ?insn, ?expDimsVal, ?len),
  (?axisNum > ?len ; -?len - 1 > ?axisNum).

ExpandDimsOpDim(?ctx, ?insn, ?expDimsVal, ?axisNum):-
  ExpandDimsValidAxisNum(?ctx, ?insn, ?expDimsVal),
  ExpandDimsOpAxisNum(?ctx, ?insn, ?expDimsVal, ?axisNum),
  ?axisNum >= 0.

ExpandDimsOpDim(?ctx, ?insn, ?expDimsVal, ?dim):-
  ExpandDimsValidAxisNum(?ctx, ?insn, ?expDimsVal),
  ExpandDimsOpInTensorShapeLength(?ctx, ?insn, ?expDimsVal, ?len),
  ExpandDimsOpAxisNum(?ctx, ?insn, ?expDimsVal, ?axisNum),
  ?axisNum < 0,
  ?dim = ?axisNum + 1 +  ?len.

TensorOperationProducesOutput(?ctx, ?insn, ?expDimsVal):-
  ExpandDimsValidAxisNum(?ctx, ?insn, ?expDimsVal).

TensorShapeListLength(?shapeHctx, ?tensorShapeVal, ?len + 1):-
  ExpandDimsOp(?ctx, ?insn, ?expDimsVal),
  TensorOperationProducesOutput(?ctx, ?insn, ?expDimsVal),
  ExpandDimsOpInTensorShapeLength(?ctx, ?insn, ?expDimsVal, ?len),
  AssignInstruction_To(?insn, ?var),
  VarPointsTo(?hctx, ?expDimsVal, ?ctx, ?var),
  TensorShape(?hctx, ?expDimsVal, ?shapeHctx, ?tensorShapeVal).

TensorShapeListContent(?shapeHctx, ?tensorShapeVal, ?index, ?indexVal):-
  ExpandDimsOpDim(?ctx, ?insn, ?expDimsVal, ?dim),
  ?index < ?dim,
  TensorOperationProducesOutput(?ctx, ?insn, ?expDimsVal),
  AssignInstruction_To(?insn, ?var),
  VarPointsTo(?hctx, ?expDimsVal, ?ctx, ?var),
  TensorShape(?hctx, ?expDimsVal, ?shapeHctx, ?tensorShapeVal),
  TensorOperation_NameToVal(?ctx, ?insn, ?expDimsVal, "input", ?tensorHctx, ?tensorArgVal),
  TensorShape(?tensorHctx, ?tensorArgVal, ?inShapeHctx, ?inTensorShape),
  TensorShapeListContent(?inShapeHctx, ?inTensorShape, ?index, ?indexVal).

TensorShapeListContent(?shapeHctx, ?tensorShapeVal, ?index + 1, ?indexVal):-
  ExpandDimsOpDim(?ctx, ?insn, ?expDimsVal, ?dim),
  ?index > ?dim,
  TensorOperationProducesOutput(?ctx, ?insn, ?expDimsVal),
  AssignInstruction_To(?insn, ?var),
  VarPointsTo(?hctx, ?expDimsVal, ?ctx, ?var),
  TensorShape(?hctx, ?expDimsVal, ?shapeHctx, ?tensorShapeVal),
  TensorOperation_NameToVal(?ctx, ?insn, ?expDimsVal, "input", ?tensorHctx, ?tensorArgVal),
  TensorShape(?tensorHctx, ?tensorArgVal, ?inShapeHctx, ?inTensorShape),
  TensorShapeListContent(?inShapeHctx, ?inTensorShape, ?index, ?indexVal).


Value_Num("<num-constant(int):1>", "1"),
TensorShapeListContent(?shapeHctx, ?tensorShapeVal, ?index, "<num-constant(int):1>"):-
  ExpandDimsOpDim(?ctx, ?insn, ?expDimsVal, ?dim),
  ?index = ?dim,
  TensorOperationProducesOutput(?ctx, ?insn, ?expDimsVal),
  AssignInstruction_To(?insn, ?var),
  VarPointsTo(?hctx, ?expDimsVal, ?ctx, ?var),
  TensorShape(?hctx, ?expDimsVal, ?shapeHctx, ?tensorShapeVal).

//----------------------------------------------------------------------------------------------------------------------
//                                              REVERSE OP
//----------------------------------------------------------------------------------------------------------------------



//----------------------------------------------------------------------------------------------------------------------
//                                              TRANSPOSE OP
//----------------------------------------------------------------------------------------------------------------------

.decl TransposeOp(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?transposeOpVal:Value)
.output TransposeOp

.decl TransposeOpShapeListLengths(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?transposeOpVal:Value, ?tensorShapeListLength:number, ?permutationListLen:number)
.output TransposeOpShapeListLengths

.decl ValidTransposeOp(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?transposeOpVal:Value, ?len:number)
.output ValidTransposeOp

TensorOperation_GetDTypeFromName(?ctx, ?insn, ?transposeOpVal, "a"),
TensorOperation_NameToVal(?ctx, ?insn, ?transposeOpVal, "a", ?tensorHctx, ?tensorArgVal),
TensorOperation_NameToVal(?ctx, ?insn, ?transposeOpVal, "perm", ?permListHctx, ?perList),
TransposeOp(?ctx, ?insn, ?transposeOpVal):-
  FunctionInvResolvesTo(?ctx, ?insn, _, _, "<tensorflow.transpose>"),
  ResolvedActualParamValue(?ctx, ?insn, "<tensorflow.transpose>", "a", ?tensorHctx, ?tensorArgVal),
  ResolvedActualParamValue(?ctx, ?insn, "<tensorflow.transpose>", "perm", ?permListHctx, ?perList),
  Instruction_Function(?insn, ?fun),
  Instruction_Index(?insn, ?index),
  ?transposeOpVal = ReshapeValueMacro("set_shape", ?fun, ?index, ?tensorArgVal, ?perList).

ValidTransposeOp(?ctx, ?insn, ?transposeOpVal, ?len):-
  TransposeOp(?ctx, ?insn, ?transposeOpVal),
  TensorOperation_NameToVal(?ctx, ?insn, ?transposeOpVal, "a", ?tensorHctx, ?tensorArgVal),
  Value_Type(?tensorArgVal, "<tensorflow.Tensor>"),
  TensorLikeObjectShapeListLength(?tensorHctx, ?tensorArgVal, ?len),
  TensorOperation_NameToVal(?ctx, ?insn, ?transposeOpVal, "perm", ?permListHctx, ?perList),
  ListOfIntConsts(?permListHctx, ?perList),
  ListOfConstsLength(?permListHctx, ?perList, ?len).

//----------------------------------------------------------------------------------------------------------------------
//                                              SET SHAPE OP
//----------------------------------------------------------------------------------------------------------------------

.decl SetShapeOp(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?setShapeOpVal:Value)
.output SetShapeOp

.decl SetShapeOpShapeListLengths(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?setShapeOpVal:Value, ?tensorShapeListLength:number, ?inShapeListLength:number)
.output SetShapeOpShapeListLengths

.decl SetShapeOpValidCorrectTypeArgs(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?setShapeOpVal:Value)
.output SetShapeOpValidCorrectTypeArgs

.decl SetShapeOpValidUpToDim(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?setShapeOpVal:Value, ?dim:number)
.output SetShapeOpValidUpToDim

.decl SetShapeOpValidForDim(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?setShapeOpVal:Value, ?dim:number, ?valForDim:Value)
.output SetShapeOpValidForDim

.decl SetShapeOpInvalidForDim(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?setShapeOpVal:Value, ?dim:number)
.output SetShapeOpInvalidForDim

.decl SetShapeOpInvalid(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?setShapeOpVal:Value)
.output SetShapeOpInvalid

.decl SetShapeOpSameListLen(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?setShapeOpVal:Value)
.output SetShapeOpSameListLen


TensorOperation_GetDTypeFromName(?ctx, ?insn, ?setShapeOpVal, "self"),
TensorOperation_NameToVal(?ctx, ?insn, ?setShapeOpVal, "self", ?tensorHctx, ?tensorArgVal),
TensorOperation_NameToVal(?ctx, ?insn, ?setShapeOpVal, "shape", ?shapeListHctx, ?shapeList),
SetShapeOp(?ctx, ?insn, ?setShapeOpVal):-
  FunctionInvResolvesTo(?ctx, ?insn, _, _, "<tensorflow.Tensor.set_shape>"),
  ResolvedActualParamValue(?ctx, ?insn, "<tensorflow.Tensor.set_shape>", "self", ?tensorHctx, ?tensorArgVal),
  ResolvedActualParamValue(?ctx, ?insn, "<tensorflow.Tensor.set_shape>", "shape", ?shapeListHctx, ?shapeList),
  Instruction_Function(?insn, ?fun),
  Instruction_Index(?insn, ?index),
  ?setShapeOpVal = ReshapeValueMacro("set_shape", ?fun, ?index, ?tensorArgVal, ?shapeList).

SetShapeOpShapeListLengths(?ctx, ?insn, ?setShapeOpVal, ?len1, ?len2),
SetShapeOpValidCorrectTypeArgs(?ctx, ?insn, ?setShapeOpVal):-
  SetShapeOp(?ctx, ?insn, ?setShapeOpVal),
  TensorOperation_NameToVal(?ctx, ?insn, ?setShapeOpVal, "self", ?tensorHctx, ?tensorArgVal),
  TensorOperation_NameToVal(?ctx, ?insn, ?setShapeOpVal, "shape", ?shapeListHctx, ?shapeList),
  Value_Type(?tensorVal, "<tensorflow.Tensor>"),
  TensorLikeObjectShapeListLength(?tensorHctx, ?tensorArgVal, ?len1),
  ListOfConstsLength(?shapeListHctx, ?shapeList, ?len2).

SetShapeOpInvalid(?ctx, ?insn, ?setShapeOpVal):-
  SetShapeOpShapeListLengths(?ctx, ?insn, ?setShapeOpVal, ?len1, ?len2),
  ?len1 != ?len2.

SetShapeOpValidForDim(?ctx, ?insn, ?setShapeOpVal, ?dim, ?shapeValForDim):-
  SetShapeOpShapeListLengths(?ctx, ?insn, ?setShapeOpVal, ?len, ?len),
  TensorOperation_NameToVal(?ctx, ?insn, ?setShapeOpVal, "self", ?tensorHctx, ?tensorArgVal),
  TensorLikeObjectShapeListContent(?tensorHctx, ?tensorArgVal, ?dim, ?tensorValForDim),
  TensorOperation_NameToVal(?ctx, ?insn, ?setShapeOpVal, "shape", ?shapeListHctx, ?shapeList),
  ListOfConstsAtIndexPointsTo(?shapeListHctx, ?shapeList, ?dim, ?shapeValForDim),
  (?shapeValForDim = ?tensorValForDim ; ?tensorValForDim = "<None>").

SetShapeOpInvalidForDim(?ctx, ?insn, ?setShapeOpVal, ?dim):-
  SetShapeOpShapeListLengths(?ctx, ?insn, ?setShapeOpVal, ?len, ?len),
  TensorOperation_NameToVal(?ctx, ?insn, ?setShapeOpVal, "self", ?tensorHctx, ?tensorArgVal),
  TensorLikeObjectShapeListContent(?tensorHctx, ?tensorArgVal, ?dim, ?tensorValForDim),
  TensorOperation_NameToVal(?ctx, ?insn, ?setShapeOpVal, "shape", ?shapeListHctx, ?shapeList),
  ListOfConstsAtIndexPointsTo(?shapeListHctx, ?shapeList, ?dim, ?shapeValForDim),
  ?shapeValForDim != ?tensorValForDim,
  ?tensorValForDim != "<None>".

SetShapeOpValidUpToDim(?ctx, ?insn, ?setShapeOpVal, 0):-
  SetShapeOpValidForDim(?ctx, ?insn, ?setShapeOpVal, 0, _).

SetShapeOpValidUpToDim(?ctx, ?insn, ?setShapeOpVal, ?dim):-
  SetShapeOpValidForDim(?ctx, ?insn, ?setShapeOpVal, ?dim, _),
  SetShapeOpValidUpToDim(?ctx, ?insn, ?setShapeOpVal, ?dim - 1).

TensorOperationProducesOutput(?ctx, ?insn, ?setShapeOpVal):-
  SetShapeOpShapeListLengths(?ctx, ?insn, ?setShapeOpVal, ?len, ?len),
  SetShapeOpValidUpToDim(?ctx, ?insn, ?setShapeOpVal, ?len - 1).


TensorShapeListContent(?shapeHctx, ?tensorShapeVal, ?index, ?indexVal):-
  TensorOperationProducesOutput(?ctx, ?insn, ?setShapeOpVal),
  SetShapeOp(?ctx, ?insn, ?setShapeOpVal),
  SetShapeOpValidForDim(?ctx, ?insn, ?setShapeOpVal, ?index, ?indexVal),
  AssignInstruction_To(?insn, ?var),
  VarPointsTo(?hctx, ?setShapeOpVal, ?ctx, ?var),
  TensorShape(?hctx, ?setShapeOpVal, ?shapeHctx, ?tensorShapeVal).

TensorShapeListLength(?shapeHctx, ?tensorShapeVal, ?len):-
  TensorOperationProducesOutput(?ctx, ?insn, ?setShapeOpVal),
  SetShapeOpShapeListLengths(?ctx, ?insn, ?setShapeOpVal, ?len, ?len),
  AssignInstruction_To(?insn, ?var),
  VarPointsTo(?hctx, ?setShapeOpVal, ?ctx, ?var),
  TensorShape(?hctx, ?setShapeOpVal, ?shapeHctx, ?tensorShapeVal).

SetShapeOpSameListLen(?ctx, ?insn, ?setShapeOpVal):-
  SetShapeOpShapeListLengths(?ctx, ?insn, ?setShapeOpVal, ?len, ?len).

//NOTE: This is hacky but I couldn't think of a better way to do it.
//Basically transfer the result of the set_shape operation to all vars pointing to the original tensor value.
//Under the same context, could change it to under any contexts, probably more correct
#if(0)
VarPointsTo(?hctx, ?setShapeOpVal, ?otherCtx, ?var):-
  SetShapeOpSameListLen(?ctx, ?insn, ?setShapeOpVal),
  TensorOperationProducesOutput(?ctx, ?insn, ?setShapeOpVal),
  AssignInstruction_To(?insn, ?myVar),
  VarPointsTo(?hctx, ?setShapeOpVal, ?ctx, ?myVar),
  TensorOperation_NameToVal(?ctx, ?insn, ?setShapeOpVal, "self", ?tensorHctx, ?tensorArgVal),
  VarPointsTo(?tensorHctx, ?tensorArgVal, ?otherCtx, ?var).

#endif

TensorShape(?tensorHctx, ?tensorArgVal, ?newShapeHctx, ?newShape):-
  SetShapeOpSameListLen(?ctx, ?insn, ?setShapeOpVal),
  TensorOperationProducesOutput(?ctx, ?insn, ?setShapeOpVal),
  AssignInstruction_To(?insn, ?myVar),
  VarPointsTo(?hctx, ?setShapeOpVal, ?ctx, ?myVar),
  TensorOperation_NameToVal(?ctx, ?insn, ?setShapeOpVal, "self", ?tensorHctx, ?tensorArgVal),
  TensorShape(?hctx, ?setShapeOpVal, ?newShapeHctx, ?newShape).