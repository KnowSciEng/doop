/**
 * Dynamic analysis
 */

HeapAllocation:Dynamic(?heap) <- DynamicHeapAllocation(_, ?heap).

Value:byDynamicHeap[?heap] = Value:byHeap[?mergeHeap] <-
   HeapAllocation:Merge[?heap] = ?mergeHeap,
   HeapAllocation:Dynamic(?heap).

Value:byDynamicHeap[?heap] = Value:byHeap[?heap] <-
   !HeapAllocation:Merge[?heap] = _,
   HeapAllocation:Dynamic(?heap).

StaticFieldPointsTo(?hctx, Value:byDynamicHeap[?heap], ?signature) <-
   DynamicStaticFieldPointsTo(?heap, ?signature),
   ImmutableHContext(?hctx).
   
InstanceFieldPointsTo(?hctx, Value:byDynamicHeap[?heap], ?fld, ?hctx, Value:byDynamicHeap[?baseHeap]) <-
   DynamicInstanceFieldPointsTo(?heap, ?fld, ?baseHeap),
   ImmutableHContext(?hctx).
   
ArrayIndexPointsTo(?hctx, Value:byDynamicHeap[?heap], ?hctx, Value:byDynamicHeap[?baseHeap]) <-
   DynamicArrayIndexPointsTo(?heap, ?baseHeap),
   ImmutableHContext(?hctx).

OptClassInitializer(?class, ?method) <- ClassInitializer[?class] = ?method.


InitializedClass(?class) <-
   DynamicReachableMethod(?method),
   OptClassInitializer(?class, ?method).

ImplicitReachable(?method) <-
   UnmatchedDynamicCallGraphEdgeNode(?method),
   DynamicReachableMethod(?method),
   !OptClassInitializer(_, ?method).

CallGraphEdge(?ctx, ?invocation, ?ctx, ?toMethod) <-
   ReachableContext(?ctx, ?method),
   !StaticMethodInvocation:Insn(?invocation),
   DynamicCallGraphEdge(?toMethod, ?invocation, ?method).
