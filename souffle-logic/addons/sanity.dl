.decl Sanity(s:symbol, c:number)
.output Sanity

// Every var has a type
Sanity("Var has no type", ?c) :-
  ?c = count : VarHasNoType(_).

// Every var in VarPointsTo is reachable
Sanity("Var in VarPointsTo is not reachable", ?c) :-
  ?c = count : NotReachableVarPointsTo(_, _).

// Every var in VarPointsTo points to correctly typed values
Sanity("Var in VarPointsTo points to wrongly typed values", ?c) :-
  ?c = count : VarPointsToWronglyTypedValue(_, _, _, _).

// No VarPointsTo is ever inferred for objects that should have been
// merged. Suggests omission in the "Keep" predicate.
Sanity("VarPointsTo for merged objects", ?c) :-
  ?c = count : VarPointsToMergedHeap(_, _).

Sanity("HeapAllocation has no type", ?c) :-
  ?c = count : HeapAllocationHasNoType(_).

Sanity("Value has no type", ?c) :-
  ?c = count : ValueHasNoType(_).

Sanity("Value is neither heap nor nonheap", ?c) :-
  ?c = count : ValueIsNeitherHeapNorNonHeap(_).

Sanity("ClassType is InterfaceType", ?c) :-
  ?c = count : ClassTypeIsInterfaceType(_).

Sanity("PrimitiveType is ReferenceType", ?c) :-
  ?c = count : PrimitiveTypeIsReferenceType(_).

// Every type is a concrete type
Sanity("Type is not a concrete type", ?c) :-
  ?c = count : TypeIsNotConcreteType(_).

// Every instruction is a concrete instruction
Sanity("Instruction is not a concrete instruction", ?c) :-
  ?c = count : InstructionIsNotConcreteInstruction(_).


.decl VarHasNoType(var:Var)
.output VarHasNoType

VarHasNoType(?var) :-
  isVar(?var),
  !Var_Type(?var, _).

.decl NotReachableVarPointsTo(?var:Var, ?method:Method)
.output NotReachableVarPointsTo

NotReachableVarPointsTo(?var, ?method) :-
  Var_DeclaringMethod(?var, ?method),
  VarPointsTo(_, _, _, ?var),
  !Reachable(?method).

.decl VarPointsToWronglyTypedValue(?var:Var, ?varType:Type, ?value:Value, ?valueType:Type)
.output VarPointsToWronglyTypedValue

VarPointsToWronglyTypedValue(?var, ?varType, ?value, ?valueType) :-
  Value_Type(?value, ?valueType),
  VarPointsTo(_, ?value, _, ?var),
  Var_Type(?var, ?varType),
  !SubtypeOf(?valueType, ?varType).

.decl VarPointsToMergedHeap(?var:Var, ?value:Value)
.output VarPointsToMergedHeap

VarPointsToMergedHeap(?var, ?value) :-
  Value_isHeap(?value),
  HeapAllocation_Merge(?value, ?value2),
  VarPointsTo(_, ?value, _, ?var),
  ?value != ?value2.

.decl HeapAllocationHasNoType(?heap:HeapAllocation)
.output HeapAllocationHasNoType

HeapAllocationHasNoType(?heap) :-
  isHeapAllocation(?heap),
  !HeapAllocation_Type(?heap, _).

.decl ValueHasNoType(?value:Value)
.output ValueHasNoType

ValueHasNoType(?value) :-
  isValue(?value),
  !Value_Type(?value, _).

.decl ValueIsNeitherHeapNorNonHeap(?value:Value)
.output ValueIsNeitherHeapNorNonHeap

ValueIsNeitherHeapNorNonHeap(?value) :-
  isValue(?value),
  !Value_isHeap(?value),
  !Value_isNonHeap(?value).

.decl ClassTypeIsInterfaceType(?type:Type)
.output ClassTypeIsInterfaceType

ClassTypeIsInterfaceType(?t) :-
  isClassType(?t),
  isInterfaceType(?t).

.decl PrimitiveTypeIsReferenceType(?type:Type)
.output PrimitiveTypeIsReferenceType

PrimitiveTypeIsReferenceType(?t) :-
    isPrimitiveType(?t),
    isReferenceType(?t).

.decl TypeIsNotConcreteType(?type:Type)
.output TypeIsNotConcreteType

TypeIsNotConcreteType(?t) :-
  isType(?t),
  !isInterfaceType(?t),
  !isClassType(?t),
  !isArrayType(?t),
  !isPrimitiveType(?t),
  !isNullType(?t).

.decl InstructionIsNotConcreteInstruction(?insn:Instruction)
.output InstructionIsNotConcreteInstruction

InstructionIsNotConcreteInstruction(?insn) :-
  isInstruction(?insn),
  !isThrow_Insn(?insn),
  !isThrowNull_Insn(?insn),
  !isGoto_Insn(?insn),
  !isIf_Insn(?insn),
  !isSwitch_Insn(?insn),
  !isTableSwitch_Insn(?insn),
  !isLookupSwitch_Insn(?insn),
  !isMonitorInstruction(?insn),
  !isEnterMonitor_Insn(?insn),
  !isExitMonitor_Insn(?insn),
  !isFieldInstruction(?insn),
  !isLoadInstanceField_Insn(?insn),
  !isStoreInstanceField_Insn(?insn),
  !isLoadStaticField_Insn(?insn),
  !isStoreStaticField_Insn(?insn),
  !isArrayInstruction(?insn),
  !isLoadArrayIndex_Insn(?insn),
  !isStoreArrayIndex_Insn(?insn),
  !isAssignInstruction(?insn),
  !isAssignOper_Insn(?insn),
  !isAssignBinop_Insn(?insn),
  !isAssignUnop_Insn(?insn),
  !isAssignLocal_Insn(?insn),
  !isAssignInstanceOf_Insn(?insn),
  !isAssignNull_Insn(?insn),
  !isAssignNumConstant_Insn(?insn),
  !isAssignCast_Insn(?insn),
  !isAssignCastNull_Insn(?insn),
  !isAssignCastNumConstant_Insn(?insn),
  !isAssignHeapAllocation_Insn(?insn),
  !isReturnInstruction(?insn),
  !isReturnVoid_Insn(?insn),
  !isReturnNonvoid_Insn(?insn),
  !isMethodInvocation(?insn),
  !isVirtualMethodInvocation_Insn(?insn),
  !isSpecialMethodInvocation_Insn(?insn),
  !isStaticMethodInvocation_Insn(?insn),
  !isUnsupportedInstruction(?insn),
  !isAssignPhantomInvoke(?insn),
  !isPhantomInvoke(?insn),
  !isBreakpointStmt(?insn).
