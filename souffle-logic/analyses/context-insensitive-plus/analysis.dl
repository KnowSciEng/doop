// Context-insensitive with an enhancement for low-hanging fruit:
// methods that have their params flow to their return value get a
// 1-call treatment.

#include "../../main/in-out-flow.dl"

.type Context = MethodInvocation
.type HContext

.decl MergeBasis(?ctx:Context, ?hctx:HContext, ?invo:MethodInvocation, ?value:Value)
.decl MergeStaticBasis(?ctx:Context, ?invo:MethodInvocation)

.decl CachedMerge(?invo:MethodInvocation, ?value:Value, ?ctx:Context)
.decl CachedMergeStatic(?invo:MethodInvocation, ?ctx:Context)

// For this analysis we only need one or two of the parameters that
// may influence the new context
.decl MyMergeBasis(?value:Value, ?valuetype:Type, ?invo:MethodInvocation)

MyMergeBasis(?value, ?valuetype, ?invo) :-
  MergeBasis(_, _, ?invo, ?value),
  Value_Type(?value, ?valuetype).
// Effort to optimize. Probably not worth it.

.decl _InOutFlowMethod(?method:Method)

_InOutFlowMethod(?method) :-
  InOutFlowMethod(_, ?method).
  
isContext(?invo) :-
  CachedMerge(_, _, ?invo).

CachedMerge(?invo, ?value, ?invo) :-
  MyMergeBasis(?value, ?valuetype, ?invo),
  ResolveInvocation(?valuetype, ?invo, ?tomethod),
  _InOutFlowMethod(?tomethod).

CachedMerge(?invo, ?value, ?calleeCtx) :-
  MyMergeBasis(?value, ?valuetype, ?invo),
  ResolveInvocation(?valuetype, ?invo, ?tomethod),
  !_InOutFlowMethod(?tomethod),
  MergeMacro(?notused, ?notused, ?notused, ?notused, ?calleeCtx).

isContext(?invo) :-
  CachedMerge(_, _, ?invo).

CachedMerge(?invo, ?value, ?invo) :-
  MyMergeBasis(?value, _, ?invo),
  isSpecialMethodInvocation_Insn(?invo),
  MethodInvocation_Method(?invo, ?tomethod),
  _InOutFlowMethod(?tomethod).

CachedMerge(?invo, ?value, ?calleeCtx) :-
  MyMergeBasis(?value, _, ?invo),
  isSpecialMethodInvocation_Insn(?invo),
  MethodInvocation_Method(?invo, ?tomethod),
  !_InOutFlowMethod(?tomethod),
  MergeMacro(?notused, ?notused, ?notused, ?notused, ?calleeCtx).

isContext(?invo) :-
  CachedMergeStatic(_, ?invo).

CachedMergeStatic(?invo, ?invo) :-
  MergeStaticBasis(_, ?invo),
  MethodInvocation_Method(?invo, ?tomethod),
  _InOutFlowMethod(?tomethod).

CachedMergeStatic(?invo, ?calleeCtx) :-
  MergeStaticBasis(_, ?invo),
  MethodInvocation_Method(?invo, ?tomethod),
  !_InOutFlowMethod(?tomethod),
  MergeStaticMacro(?notused, ?notused, ?calleeCtx).
