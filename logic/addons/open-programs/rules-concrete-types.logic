#include "macros.logic"
#include "entry-points.logic"

FormalParamOpt(?index, ?method, ?formal) <- FormalParam[?index, ?method] = ?formal.

AllConcreteImplementations(?class, ?staticType) <-
   SubtypeOf(?class, ?staticType),
   ClassType(?class),
   !ClassModifier("abstract", ?class).

AllConcreteImplementations(?class, ?class) <-
   ClassType(?class),
   !ClassModifier("abstract", ?class).

AllConcreteImplementations(?type, ?type) <-
   ArrayType(?type).

ConcreteImplementations[?staticType] = ?class <-
   agg << ?class = min(?allClasses) >> AllConcreteImplementations(?allClasses, ?staticType).

AnyConcreteImplementations(?class, ?staticType) <-
   ConcreteImplementations[?staticType] = ?class.

MockObjectIntroduced(?class, ?value) <- ObjToMock(?class).

ObjToMock(?class) -> ReferenceType(?class).

// invariant: this should always be a type class
MockObjFromEntryPoint(?class),
ObjToMock(?class) <-
   EntryPoint(?method),
   Method:DeclaringType[?method] = ?class,
   !ClassModifier("abstract", ?class),
   ReferenceType(?class).

MockObjFromField(?fieldType),
ObjToMock(?fieldType) <-
   FieldInformationOpt(_, _, ?fieldType),
   !ClassModifier("abstract", ?fieldType),
   ReferenceType(?fieldType).

MockObjFromArray(?class),
ObjToMock(?class) <-
   MockObject(_, ?type),
   ArrayType(?type),
   ComponentType[?type] = ?componentType,
   AnyConcreteImplementations(?class, ?componentType).

FieldInformation(?signature, ?fieldtype, ?class) ->
   Field(?signature), Type(?fieldtype), Type(?class).

FieldInformation(?signature, ?fieldtype, ?class) <-
   Field:Type[?signature] = ?fieldtype,
   Field:DeclaringType[?signature] = ?class,
   MockObject(_, ?class).

FieldInformation(?signature, ?fieldtype, ?class) <-
   FieldInformation(?signature, ?fieldtype, ?staticType),
   AllConcreteImplementations(?class, ?staticType),
   MockObject(_, ?class).

FieldInformation(?signature, ?fieldConcreteType, ?class) <-
   FieldInformationOpt(?signature, ?class, ?fieldStaticType),
   AnyConcreteImplementations(?fieldConcreteType, ?fieldStaticType).

FieldInformationOpt(?signature, ?class, ?fieldtype) <-
   FieldInformation(?signature, ?fieldtype, ?class).
   
MockValueForField(?signature, ?basevalue, ?fieldtype) ->
   Field(?signature), Type(?fieldtype), Value(?basevalue).

MockValueForField(?signature, ?basevalue, ?fieldtype) <-
   FieldInformation(?signature, ?fieldtype, ?class),
   MockObject(?basevalue, ?class).

MockObjectOpt(?fieldtype, ?value) <- MockObject(?value, ?fieldtype).

#include "entry-points.logic"
 
InstanceFieldPointsTo(?hctx, ?value, ?signature, ?hctx, ?basevalue) <-
   ImmutableHContext(?hctx),
   MockValueForField(?signature, ?basevalue, ?fieldtype),
   MockObject(?value, ?fieldtype).

ArrayIndexPointsTo(?hctx, ?componentValue, ?hctx, ?arrayValue) <-
   MockObject(?arrayValue, ?arrayType),
   ArrayType(?arrayType),
   ComponentType[?arrayType] = ?componentType,
   AnyConcreteImplementations(?class, ?componentType),
   MockObject(?componentValue, ?class),
   ImmutableHContext(?hctx).

TypeForFormals(?type, ?index, ?method) -> ReferenceType(?type), int[32](?index), Method(?method).
   
ObjToMock(?subtype),
TypeForFormals(?subtype, ?index, ?method) <-
   FormalParam[?index, ?method] = ?formal,
   Var:Type[?formal] = ?staticType,
   AnyConcreteImplementations(?subtype, ?staticType),
   EntryPoint(?method).

// bind mock objects to formals
ValueForFormalInContext(?ctx, ?formal, ?value) <-
   TypeForFormals(?class, ?index, ?method),
   MockObject(?value, ?class),
   ReachableContext(?ctx, ?method),
   FormalParam[?index, ?method] = ?formal.

VarPointsTo(?hctx, ?value, ?ctx, ?formal)
 <-
   ValueForFormalInContext(?ctx, ?formal, ?value),
   //   ImmutableHContextFromContext[?ctx] = ?hctx. // TODO multiple strategies for this
   ImmutableHContext(?hctx).

// receivers for entry points
MergeStartupMacro(?hctx, ?value, ?calleeCtx),
VarPointsTo(?hctx, ?value, ?calleeCtx, ?receiver),
ReachableContext(?calleeCtx, ?method),
MockReachableContext(?calleeCtx, ?method)
 <-
   ThisVar[?method] = ?receiver,
   Method:DeclaringType[?method] = ?class,
   MockObject(?value, ?class),
   EntryPoint(?method),
   ImmutableHContext(?hctx).

CallGraphEdgeOpt(?calleeCtx, ?method, ?callerCtx, ?invocation)
 <- CallGraphEdge(?callerCtx, ?invocation, ?calleeCtx, ?method).

MethodCallGraphEdge(?calleeCtx, ?method, ?callerCtx, ?inmethod) <-
   CallGraphEdgeOpt(?calleeCtx, ?method, ?callerCtx, ?invocation),
   Instruction:Method[?invocation] = ?inmethod.

MockReachableContext(?calleeCtx, ?method) <-
   MethodCallGraphEdge(?calleeCtx, ?method, ?callerCtx, ?inmethod),
   MockReachableContext(?callerCtx, ?inmethod).

// bind mock objects to receivers
ValueForReceiverInContext(?ctx, ?receiver, ?value) <-
   MockReachableContext(?ctx, ?method),
   ThisVar[?method] = ?receiver,
   Method:DeclaringType[?method] = ?class,
   MockObject(?value, ?class).

// all other receivers
VarPointsTo(?hctx, ?value, ?ctx, ?receiver)
 <-
   ValueForReceiverInContext(?ctx, ?receiver, ?value),
   ImmutableHContext(?hctx).