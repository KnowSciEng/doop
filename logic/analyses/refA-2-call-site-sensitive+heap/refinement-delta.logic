#include "macros.logic"

// Avoid refining at invocation sites that pass more than N objects as arguments.
_ContextInsensVPT(?value, ?var) -> Var(?var), Value(?value).
+_ContextInsensVPT(?value, ?var) <- VarPointsTo(_, ?value, _, ?var).

_HeapsPerInvocationPerArg(?value, ?arg, ?invocation) ->
  Var(?arg), MethodInvocation(?invocation), Value(?value).
+_HeapsPerInvocationPerArg(?value, ?arg, ?invocation) <-
  CallGraphEdge(_, ?invocation, _, _),
  ActualParam[_, ?invocation] = ?arg,
  _ContextInsensVPT(?value, ?arg).

_NumHeapsForInvocationArgs[?invocation] = ?count ->
  MethodInvocation(?invocation), int[64](?count).
+_NumHeapsForInvocationArgs[?invocation] = ?count <-
  agg<<?count = count()>>(_HeapsPerInvocationPerArg(_, _, ?invocation)).

+TempSiteToRefine(?invocation) <-
  _NumHeapsForInvocationArgs[?invocation] > 1000.

// Avoid refining method calls that refer to methods containing variables that point
// to heap objects with a high max number of field-points-to facts. This is what
// causes explosion for deep-context analyses.
_FieldPointsToPerAllocSiteAndField[?alloc, ?fld] = ?count -> Value(?alloc), int[64](?count), Field(?fld).
+_FieldPointsToPerAllocSiteAndField[?alloc, ?fld] = ?count <-
  agg<<?count = count()>>(InstanceFieldPointsTo(_, _, ?fld, _, ?alloc)).

_MaxFieldPointsToPerAllocSite[?alloc] = ?count -> Value(?alloc), int[64](?count).
+_MaxFieldPointsToPerAllocSite[?alloc] = ?count <-
  agg<<?count = max(?v)>>(_FieldPointsToPerAllocSiteAndField[?alloc,_] = ?v).

+TempSiteToRefine(?invocation) <-
  CallGraphEdge(_, ?invocation, _, ?tomethod),
  VarPointsTo(_, ?alloc, _, ?var),
  _MaxFieldPointsToPerAllocSite[?alloc] > 3000,
  Var:DeclaringMethod(?var, ?tomethod).

+TempNegativeSiteFilter[] = true.


// Avoid refining allocation sites pointed to by more than K vars already. Refine all the rest.
_VarsPerAllocSite[?alloc] = ?count -> Value(?alloc), int[64](?count).
+_VarsPerAllocSite[?alloc] = ?count <-
  agg<<?count = count()>>(VarPointsTo(_, ?alloc, _, _)).

+TempValueToRefine(?alloc) <-
  _VarsPerAllocSite[?alloc] = ?count,
  ?count > 1500.

+TempNegativeObjectFilter[] = true.
