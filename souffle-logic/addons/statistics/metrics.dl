.decl ReachableClass(?class:symbol)
.decl ReachableApplicationClass(?class:symbol)
.decl ReachableSDKClass(?class:symbol)
.decl ReachableDependencyClass(?class:symbol)
.decl DependencyMethodDirectlyReachableFromApplication(?method:symbol)
.decl DependencyMethodDirectlyReachableFromDependency(?method:symbol)
.decl DependencyMethodDirectlyReachableFromSDK(?method:symbol)
.decl SDKMethodDirectlyReachableFromApplication(?method:symbol)
.decl SDKMethodDirectlyReachableFromDependency(?method:symbol)
.decl SDKMethodDirectlyReachableFromSDK(?method:symbol)

.output ReachableClass
.output ReachableApplicationClass
.output ReachableSDKClass
.output ReachableDependencyClass
.output DependencyMethodDirectlyReachableFromApplication
.output DependencyMethodDirectlyReachableFromDependency
.output DependencyMethodDirectlyReachableFromSDK
.output SDKMethodDirectlyReachableFromApplication
.output SDKMethodDirectlyReachableFromDependency
.output SDKMethodDirectlyReachableFromSDK

ReachableClass(?class) :-
    Reachable(?method),
    Method_DeclaringType(?method, ?class).

ReachableApplicationClass(?class) :-
    Reachable(?method),
    Method_DeclaringType(?method, ?class),
    ApplicationClass(?class).

ReachableSDKClass(?class) :-
    Reachable(?method),
    Method_DeclaringType(?method, ?class),
    (match("android.*", ?class);
     match("java.*", ?class)),
    !ApplicationClass(?class).

ReachableDependencyClass(?class) :-
    Reachable(?method),
    Method_DeclaringType(?method, ?class),
    !ApplicationClass(?class),
    !ReachableSDKClass(?class).

DependencyMethodDirectlyReachableFromApplication(?depMethod) :-
    Reachable(?appMethod),
    Method_DeclaringType(?appMethod, ?appClass),
    ApplicationClass(?appClass),
    MethodInvocation_Method(?invo, ?appMethod),
    CallGraphEdge(_, ?invo, _, ?depMethod),
    Method_DeclaringType(?depMethod, ?depClass),
    ReachableDependencyClass(?depClass).

DependencyMethodDirectlyReachableFromDependency(?depMethod) :-
    Reachable(?callerDepMethod),
    Method_DeclaringType(?callerDepMethod, ?callerDepClass),
    ReachableDependencyClass(?callerDepClass),
    MethodInvocation_Method(?invo, ?callerDepMethod),
    CallGraphEdge(_, ?invo, _, ?depMethod),
    Method_DeclaringType(?depMethod, ?depClass),
    ReachableDependencyClass(?depClass).

DependencyMethodDirectlyReachableFromSDK(?depMethod) :-
    Reachable(?sdkMethod),
    Method_DeclaringType(?sdkMethod, ?sdkClass),
    ReachableSDKClass(?sdkClass),
    MethodInvocation_Method(?invo, ?sdkMethod),
    CallGraphEdge(_, ?invo, _, ?depMethod),
    Method_DeclaringType(?depMethod, ?depClass),
    ReachableDependencyClass(?depClass).

SDKMethodDirectlyReachableFromApplication(?sdkMethod) :-
    Reachable(?appMethod),
    Method_DeclaringType(?appMethod, ?appClass),
    ApplicationClass(?appClass),
    MethodInvocation_Method(?invo, ?appMethod),
    CallGraphEdge(_, ?invo, _, ?sdkMethod),
    Method_DeclaringType(?sdkMethod, ?sdkClass),
    ReachableSDKClass(?sdkClass).

SDKMethodDirectlyReachableFromDependency(?sdkMethod) :-
    Reachable(?depMethod),
    Method_DeclaringType(?depMethod, ?depClass),
    ReachableDependencyClass(?depClass),
    MethodInvocation_Method(?invo, ?depMethod),
    CallGraphEdge(_, ?invo, _, ?sdkMethod),
    !SDKMethodDirectlyReachableFromApplication(?sdkMethod),
    Method_DeclaringType(?sdkMethod, ?sdkClass),
    ReachableSDKClass(?sdkClass).

SDKMethodDirectlyReachableFromSDK(?sdkMethod) :-
    Reachable(?callerSDKMethod),
    Method_DeclaringType(?callerSDKMethod, ?callerSDKClass),
    ReachableSDKClass(?callerSDKClass),
    MethodInvocation_Method(?invo, ?callerSDKMethod),
    CallGraphEdge(_, ?invo, _, ?sdkMethod),
    !SDKMethodDirectlyReachableFromApplication(?sdkMethod),
    !SDKMethodDirectlyReachableFromDependency(?sdkMethod),
    Method_DeclaringType(?sdkMethod, ?sdkClass),
    ReachableSDKClass(?sdkClass).

