#ifndef IMPORT_PARTITIONS
 #include "../../main/classes-scc.logic"
#endif
#include "../../main/in-out-flow.logic"

EmptyContext[] = ?ctx -> Context(?ctx).
lang:constructor(`EmptyContext).

// First element is most important
ContextFromOne[?comp] = ?ctx -> Context(?ctx), ContextComponent(?comp).
lang:constructor(`ContextFromOne).
FirstFromContext[?ctx] = ?comp <-
  ContextFromOne[?comp] = ?ctx.

ContextFromTwo[?comp1, ?comp2] = ?ctx -> Context(?ctx), ContextComponent(?comp1), ContextComponent(?comp2).
lang:constructor(`ContextFromTwo).
FirstFromContext[?ctx] = ?comp1,
SecondFromContext[?ctx] = ?comp2 <-
  ContextFromTwo[?comp1, ?comp2] = ?ctx.

/*
///// RESTRICT TO 2 MAX!
ContextFromThree[?comp1, ?comp2, ?comp3] = ?ctx ->
  Context(?ctx), ContextComponent(?comp1), ContextComponent(?comp2), ContextComponent(?comp3).
lang:constructor(`ContextFromThree).
FirstFromContext[?ctx] = ?comp1,
SecondFromContext[?ctx] = ?comp2,
ThirdFromContext[?ctx] = ?comp3 <-
  ContextFromThree[?comp1, ?comp2, ?comp3] = ?ctx.
*/

/*
//// Used to create an HContext per Context but this yields HContexts of length +1
HContextFromContext[?ctx] = ?hctx ->
  HContext(?hctx), Context(?ctx).
lang:constructor(`HContextFromContext).

FirstFromHContext[?hctx] = ?comp1 <-
  HContextFromContext[?ctx] = ?hctx,
  FirstFromContext[?ctx] = ?comp1.
SecondFromHContext[?hctx] = ?comp2 <-
  HContextFromContext[?ctx] = ?hctx,
  SecondFromContext[?ctx] = ?comp2.
*/

EmptyHContext[] = ?hctx -> HContext(?hctx).
lang:constructor(`EmptyHContext).

// First element is most important
HContextFromOne[?comp] = ?hctx -> HContext(?hctx), ContextComponent(?comp).
lang:constructor(`HContextFromOne).
FirstFromHContext[?hctx] = ?comp <-
  HContextFromOne[?comp] = ?hctx.

/*
HContextFromTwo[?comp1, ?comp2] = ?hctx -> HContext(?hctx), ContextComponent(?comp1), ContextComponent(?comp2).
lang:constructor(`HContextFromTwo).
FirstFromHContext[?hctx] = ?comp1,
SecondFromHContext[?hctx] = ?comp2 <-
  HContextFromTwo[?comp1, ?comp2] = ?hctx.
*/

ContextLength[?ctx] = ?len -> Context(?ctx), int[32](?len).
HContextLength[?hctx] = ?len -> HContext(?hctx), int[32](?len).
ContextLength[?ctx] = 0 <-
  EmptyContext[] = ?ctx.
ContextLength[?ctx] = 1 <-
  ContextFromOne[_] = ?ctx.
ContextLength[?ctx] = 2 <-
  ContextFromTwo[_, _] = ?ctx.
HContextLength[?hctx] = 0 <-
  EmptyHContext[] = ?hctx.
HContextLength[?hctx] = 1 <-
  HContextFromOne[_] = ?hctx.
/*
HContextLength[?hctx] = 2 <-
  HContextFromTwo[_, _] = ?hctx.
*/
/*
///// RESTRICT TO 2 MAX!
ContextLength[?ctx] = 3 <-
  ContextFromThree[_, _, _] = ?ctx.
*/
/*
HContextLength[?hctx] = ?len <-
  HContextFromContext[?ctx] = ?hctx,
  ContextLength[?ctx] = ?len.
*/


// General idea:
//  -HContexts are however precise the current calling context allows
//  -At "interesting points":
//     Calling contexts get as precise as they can be, based on hcontext,value of receiver, up to limit
//  -At "uninteresting points":
//     Calling contexts are updated but keep the same overall length as they had in the caller

// instance, reset, (0,0)
// instance, reset, (1,0)
// instance, reset, (2,0)
// instance, reset, (0,1)
// instance, reset, (1,1)
// instance, reset, (2,1)
TempoInstanceReset012:H01(?calleeCtx),
Context(?calleeCtx), EmptyContext[] = ?calleeCtx,
CachedMerge[?callerCtx, ?hctx, ?invo, ?value] = ?calleeCtx <-
  MergeBasis(?callerCtx, ?hctx, ?invo, ?value),
  PointToResetContext(?invo, ?value).

// instance, increase, (0,0)
// instance, increase, (1,0)
// instance, increase, (2,0)
TempoInstanceIncrease012:H0(?calleeCtx),
Context(?calleeCtx), ContextFromOne[?valueComp] = ?calleeCtx,
ContextComponent(?valueComp), ContextComponent:Value[?value] = ?valueComp,
CachedMerge[?callerCtx, ?hctx, ?invo, ?value] = ?calleeCtx <-
  MergeBasis(?callerCtx, ?hctx, ?invo, ?value),
  PointToIncreaseContext(?invo, ?value),
  HContextLength[?hctx] = 0.

// instance, increase, (0,1)
// instance, increase, (1,1)
// instance, increase, (2,1)
TempoInstanceIncrease012:H1(?calleeCtx),
Context(?calleeCtx), ContextFromTwo[?valueComp, FirstFromHContext[?hctx]] = ?calleeCtx,
ContextComponent(?valueComp), ContextComponent:Value[?value] = ?valueComp,
CachedMerge[?callerCtx, ?hctx, ?invo, ?value] = ?calleeCtx <-
  MergeBasis(?callerCtx, ?hctx, ?invo, ?value),
  PointToIncreaseContext(?invo, ?value),
  HContextLength[?hctx] >= 1.

/*
///// RESTRICT TO 2 MAX!
Tempo2(?calleeCtx),
Context(?calleeCtx), ContextFromTwo[?valueComp, FirstFromHContext[?hctx]] = ?calleeCtx,
ContextComponent(?valueComp), ContextComponent:Value[?value] = ?valueComp,
CachedMerge[?callerCtx, ?hctx, ?invo, ?value] = ?calleeCtx <-
  MergeBasis(?callerCtx, ?hctx, ?invo, ?value),
  PointToIncreaseContext(?invo, ?value),
  HContextLength[?hctx] = 1.

// Doesn't get longer than three
Tempo3(?calleeCtx),
Context(?calleeCtx),
ContextFromThree[?valueComp, FirstFromHContext[?hctx], SecondFromHContext[?hctx]] = ?calleeCtx,
ContextComponent(?valueComp), ContextComponent:Value[?value] = ?valueComp,
CachedMerge[?callerCtx, ?hctx, ?invo, ?value] = ?calleeCtx <-
  MergeBasis(?callerCtx, ?hctx, ?invo, ?value),
  PointToIncreaseContext(?invo, ?value),
  HContextLength[?hctx] > 1.
*/

// instance, update, (0,0)
// instance, update, (0,1)
TempoInstanceUpdate0:H01(?calleeCtx),
Context(?calleeCtx), EmptyContext[] = ?calleeCtx,
CachedMerge[?callerCtx, ?hctx, ?invo, ?value] = ?calleeCtx <-
  MergeBasis(?callerCtx, ?hctx, ?invo, ?value),
  PointToUpdateContext(?invo, ?value),
  ContextLength[?callerCtx] = 0.

// instance, update, (1,0)
// instance, update, (1,1)
TempoInstanceUpdate1:H01(?calleeCtx),
Context(?calleeCtx), ContextFromOne[?valueComp] = ?calleeCtx,
ContextComponent(?valueComp), ContextComponent:Value[?value] = ?valueComp,
CachedMerge[?callerCtx, ?hctx, ?invo, ?value] = ?calleeCtx <-
  MergeBasis(?callerCtx, ?hctx, ?invo, ?value),
  PointToUpdateContext(?invo, ?value),
  ContextLength[?callerCtx] = 1.

// instance, update, (2,0)
TempoInstanceUpdate2:H0(?calleeCtx),
Context(?calleeCtx), ContextFromOne[?valueComp] = ?calleeCtx,
ContextComponent(?valueComp), ContextComponent:Value[?value] = ?valueComp,
CachedMerge[?callerCtx, ?hctx, ?invo, ?value] = ?calleeCtx <-
  MergeBasis(?callerCtx, ?hctx, ?invo, ?value),
  PointToUpdateContext(?invo, ?value),
  HContextLength[?hctx] = 0,
  ContextLength[?callerCtx] > 1.
  // if we either just want a 1-deep context, or that's the best we can offer, do that

// instance, update, (2,1)
TempoInstanceUpdate2:H1(?calleeCtx),
Context(?calleeCtx),
ContextFromTwo[?valueComp, FirstFromHContext[?hctx]] = ?calleeCtx,
ContextComponent(?valueComp), ContextComponent:Value[?value] = ?valueComp,
CachedMerge[?callerCtx, ?hctx, ?invo, ?value] = ?calleeCtx <-
  MergeBasis(?callerCtx, ?hctx, ?invo, ?value),
  PointToUpdateContext(?invo, ?value),
  ContextLength[?callerCtx] >= 2,
  HContextLength[?hctx] >= 1.

/*
///// RESTRICT TO 2 MAX!
Tempo6(?calleeCtx),
Context(?calleeCtx),
ContextFromTwo[?valueComp, FirstFromHContext[?hctx]] = ?calleeCtx,
ContextComponent(?valueComp), ContextComponent:Value[?value] = ?valueComp,
CachedMerge[?callerCtx, ?hctx, ?invo, ?value] = ?calleeCtx <-
  MergeBasis(?callerCtx, ?hctx, ?invo, ?value),
  PointToUpdateContext(?invo, ?value),
  ((ContextLength[?callerCtx] = 2, HContextLength[?hctx] >= 1);
   (ContextLength[?callerCtx] = 3, HContextLength[?hctx] = 1)).
  // if we want 2-deep, or we want 3, but can only get two

Tempo7(?calleeCtx),
Context(?calleeCtx),
ContextFromThree[?valueComp, FirstFromHContext[?hctx], SecondFromHContext[?hctx]] = ?calleeCtx,
ContextComponent(?valueComp), ContextComponent:Value[?value] = ?valueComp,
CachedMerge[?callerCtx, ?hctx, ?invo, ?value] = ?calleeCtx <-
  MergeBasis(?callerCtx, ?hctx, ?invo, ?value),
  PointToUpdateContext(?invo, ?value),
  ContextLength[?callerCtx] = 3,
  HContextLength[?hctx] >= 2.
*/


// static, reset, 0
// static, reset, 1
// static, reset, 2
/// Now the same for static. Use invo when possible
TempoStaticReset012(?calleeCtx),
Context(?calleeCtx), EmptyContext[] = ?calleeCtx,
CachedMergeStatic[?callerCtx, ?invo] = ?calleeCtx <-
  MergeStaticBasis(?callerCtx, ?invo),
  InvocationToResetContext(?invo).

// static, increase, 0
TempoStaticIncrease0(?calleeCtx),
Context(?calleeCtx), ContextFromOne[?invoComp] = ?calleeCtx,
ContextComponent(?invoComp), ContextComponent:MethodInvocation[?invo] = ?invoComp,
CachedMergeStatic[?callerCtx, ?invo] = ?calleeCtx <-
  MergeStaticBasis(?callerCtx, ?invo),
  InvocationToIncreaseContext(?invo),
  ContextLength[?callerCtx] = 0.

// static, increase, 1
// static, increase, 2
TempoStaticIncrease12(?calleeCtx),
Context(?calleeCtx), ContextFromTwo[?invoComp, FirstFromContext[?callerCtx]] = ?calleeCtx,
ContextComponent(?invoComp), ContextComponent:MethodInvocation[?invo] = ?invoComp,
CachedMergeStatic[?callerCtx, ?invo] = ?calleeCtx <-
  MergeStaticBasis(?callerCtx, ?invo),
  InvocationToIncreaseContext(?invo),
  ContextLength[?callerCtx] >= 1.

/*
///// RESTRICT TO 2 MAX!
Tempo9(?calleeCtx),
Context(?calleeCtx), ContextFromTwo[?invoComp, FirstFromContext[?callerCtx]] = ?calleeCtx,
ContextComponent(?invoComp), ContextComponent:MethodInvocation[?invo] = ?invoComp,
CachedMergeStatic[?callerCtx, ?invo] = ?calleeCtx <-
  MergeStaticBasis(?callerCtx, ?invo),
  InvocationToIncreaseContext(?invo),
  ContextLength[?callerCtx] = 1.

Tempo10(?calleeCtx),
Context(?calleeCtx),
ContextFromThree[?invoComp, FirstFromContext[?callerCtx], SecondFromContext[?callerCtx]] = ?calleeCtx,
ContextComponent(?invoComp), ContextComponent:MethodInvocation[?invo] = ?invoComp,
CachedMergeStatic[?callerCtx, ?invo] = ?calleeCtx <-
  MergeStaticBasis(?callerCtx, ?invo),
  InvocationToIncreaseContext(?invo),
  ContextLength[?callerCtx] >= 2.
*/

// static, update, 0
TempoStaticUpdate0(?calleeCtx),
Context(?calleeCtx), EmptyContext[] = ?calleeCtx,
CachedMergeStatic[?callerCtx, ?invo] = ?calleeCtx <-
  MergeStaticBasis(?callerCtx, ?invo),
  InvocationToUpdateContext(?invo),
  ContextLength[?callerCtx] = 0.

// static, update, 1
TempoStaticUpdate1(?calleeCtx),
Context(?calleeCtx),
ContextFromOne[?invoComp] = ?calleeCtx,
ContextComponent(?invoComp), ContextComponent:MethodInvocation[?invo] = ?invoComp,
CachedMergeStatic[?callerCtx, ?invo] = ?calleeCtx <-
  MergeStaticBasis(?callerCtx, ?invo),
  InvocationToUpdateContext(?invo),
  ContextLength[?callerCtx] = 1.

// static, update, 2
TempoStaticUpdate2(?calleeCtx),
Context(?calleeCtx),
ContextFromTwo[?invoComp, FirstFromContext[?callerCtx]] = ?calleeCtx,
ContextComponent(?invoComp), ContextComponent:MethodInvocation[?invo] = ?invoComp,
CachedMergeStatic[?callerCtx, ?invo] = ?calleeCtx <-
  MergeStaticBasis(?callerCtx, ?invo),
  InvocationToUpdateContext(?invo),
  ContextLength[?callerCtx] >= 2.
/*
///// RESTRICT TO 2 MAX!
Tempo13(?calleeCtx),
Context(?calleeCtx),
ContextFromTwo[?invoComp, FirstFromContext[?callerCtx]] = ?calleeCtx,
ContextComponent(?invoComp), ContextComponent:MethodInvocation[?invo] = ?invoComp,
CachedMergeStatic[?callerCtx, ?invo] = ?calleeCtx <-
  MergeStaticBasis(?callerCtx, ?invo),
  InvocationToUpdateContext(?invo),
  ContextLength[?callerCtx] = 2.

Tempo14(?calleeCtx),
Context(?calleeCtx),
ContextFromThree[?invoComp, FirstFromContext[?callerCtx], SecondFromContext[?callerCtx]] = ?calleeCtx,
ContextComponent(?invoComp), ContextComponent:MethodInvocation[?invo] = ?invoComp,
CachedMergeStatic[?callerCtx, ?invo] = ?calleeCtx <-
  MergeStaticBasis(?callerCtx, ?invo),
  InvocationToUpdateContext(?invo),
  ContextLength[?callerCtx] = 3.
*/


HContext(?hctx), 
HContextFromOne[FirstFromContext[?ctx]] = ?hctx,
CachedRecord[?ctx, ?value] = ?hctx <-
   RecordBasis(?ctx, ?var, ?value),
   ?var = ?var, // no warning
   ContextLength[?ctx] >= 1.

HContext(?hctx), 
EmptyHContext[] = ?hctx,
CachedRecord[?ctx, ?value] = ?hctx <-
   RecordBasis(?ctx, ?var, ?value),
   ?var = ?var, // no warning
   ContextLength[?ctx] = 0.


///// ===========================================
// Base concepts: allocation sites or methods that require more context sensitivity.
// Currently use in-out flow and SCC info.

// Final output: derivative concepts

// Virtual
PointToIncreaseContext(?invo, ?value) <-
  MergeBasis(_, _, ?invo, ?value),
  VirtualMethodInvocation:Insn(?invo),
  Value:Type[?value] = ?valuetype,
  InterestingInvocationAndType(?valuetype, ?invo).

PointToResetContext(?invo, ?value) <-
  MergeBasis(_, _, ?invo, ?value),
  VirtualMethodInvocation:Insn(?invo),
  Value:Type[?value] = ?valuetype,
  UninterestingInvocationAndType(?valuetype, ?invo).

PointToUpdateContext(?invo, ?value) <-
  MergeBasis(_, _, ?invo, ?value),
  VirtualMethodInvocation:Insn(?invo),
  Value:Type[?value] = ?valuetype,
  !InterestingInvocationAndType(?valuetype, ?invo),
  !UninterestingInvocationAndType(?valuetype, ?invo).

// Special
PointToIncreaseContext(?invo, ?value) <-
  MergeBasis(_, _, ?invo, ?value),
  SpecialMethodInvocation:Insn(?invo),
  InterestingInvocation(?invo).

PointToResetContext(?invo, ?value) <-
  MergeBasis(_, _, ?invo, ?value),
  SpecialMethodInvocation:Insn(?invo),
  UninterestingInvocation(?invo).

PointToUpdateContext(?invo, ?value) <-  
  MergeBasis(_, _, ?invo, ?value),
  SpecialMethodInvocation:Insn(?invo),
  !InterestingInvocation(?invo),
  !UninterestingInvocation(?invo).

// Static
InvocationToIncreaseContext(?invo) <-
  StaticMethodInvocation:Insn(?invo),
  InterestingInvocation(?invo).

InvocationToResetContext(?invo) <-
  StaticMethodInvocation:Insn(?invo),
  UninterestingInvocation(?invo).

InvocationToUpdateContext(?invo) <-
  StaticMethodInvocation:Insn(?invo),
  !InterestingInvocation(?invo),
  !UninterestingInvocation(?invo).

/// More primitive concepts
// TODO: also add methods allocating objects that escape, based on quick analysis?

MethodWithReferenceArguments(?method) <-
  FormalParam[_, ?method] = ?formal,
  Var:Type[?formal] = ?formalType,
  ReferenceType(?formalType).

StaticMethodWithNoReferenceArguments(?method) <-
  Method(?method),
  !MethodWithReferenceArguments(?method),
  Method:Modifier("static", ?method).

InterestingInvocation(?invo) <- StaticMethodInvocation:Insn(?invo).

InterestingInvocation(?invo) <- SpecialMethodInvocation:Insn(?invo).

_OptResolveInvocation(?type, ?invo, ?tomethod) <-
   ResolveInvocation[?type, ?invo] = ?tomethod.

InterestingInvocationAndType(?type, ?invo) <-
   _OptResolveInvocation(?type, ?invo, _).

UninterestingInvocation(?invo) -> MethodInvocation(?invo).

UninterestingInvocationAndType(?type, ?invo) -> Type(?type), MethodInvocation(?invo).