Reachable(?method) <-
   MainMethodDeclaration(?method).

Assign(?to, ?from) <-
   AssignLocal(?from, ?to, _).

Assign(?formal, ?actual) <-
   CallGraphEdge(?invocation, ?toMethod),
   FormalParam[?index, ?toMethod] = ?formal,
   ActualParam[?index, ?invocation] = ?actual.

Assign(?local, ?return) <-
   CallGraphEdge(?invocation, ?toMethod),
   ReturnVar(?return, ?toMethod),
   AssignReturnValue[?invocation] = ?local.

/**
 * Var points to Value
 */

VarPointsTo(?var, ?value) <-
   AssignNormalHeapAllocation(?heap, ?var, ?inmethod),
   Reachable(?inmethod).

VarPointsTo(?var, ?value) <-
   AssignAuxiliaryHeapAllocation(?value, ?var, ?inmethod),
   Reachable(?inmethod).

VarPointsTo(?var, ?value) <-
   AssignContextInsensitiveHeapAllocation(?value, ?var, ?inmethod),
   Reachable(?inmethod).

VarPointsTo(?to, ?value) <-
   Reachable(?inmethod),
   AssignLocal(?from, ?to, ?inmethod),
   VarPointsTo(?value, ?from).

FieldPointsTo(?basevalue, ?fld, ?value) <-
   StoreInstanceField(?from, ?base, ?fld, _),
   VarPointsTo(?base, ?basevalue),
   VarPointsTo(?from, ?value).

VarPointsTo(?to, ?value) <-
   LoadInstanceField(?base, ?fld, ?to, _),
   VarPointsTo(?base, ?basevalue),
   FieldPointsTo(?basevalue, ?fld, ?value).

Reachable(?toMethod),
CallGraphEdge(?invocation, ?toMethod),
VarPointsTo(?this, ?value) <-
   Reachable(?inMethod),
   Instruction:Method[?invocation] = ?inMethod,
   VirtualMethodInvocation:Base[?invocation] = ?base,
   VarPointsTo(?base, ?value),
   Value:Type[?value] = ?valuetype,
   ResolveInvocation[?valuetype, ?invocation] = ?toMethod,
   ThisVar[?toMethod] = ?this.
