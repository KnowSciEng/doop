// Context-sensitive pointer analysis with context of 3 types and a
// context-sensitive heap abstraction.

// In this analysis, the real context is a triplet of Types and the real heap
// context a pair of Types. Keeping mapping functions is the way to handle
// analyses where HContext = Context (since the language considers them
// different types).  The type is not always a class type, because of arrays
// currently being a non-class subtype of Object
ContextFromRealContext[?type1, ?type2, ?type3] = ?ctx ->
  Context(?ctx), Type(?type1), Type(?type2), Type(?type3).
lang:constructor(`ContextFromRealContext).
RealContext1FromContext[?ctx] = ?type ->
  Context(?ctx), Type(?type).
RealContext2FromContext[?ctx] = ?type ->
  Context(?ctx), Type(?type).
RealContext3FromContext[?ctx] = ?type ->
  Context(?ctx), Type(?type).
RealContext1FromContext[?ctx] = ?type1,
RealContext2FromContext[?ctx] = ?type2,
RealContext3FromContext[?ctx] = ?type3 <-
  ContextFromRealContext[?type1, ?type2, ?type3] = ?ctx.

HContextFromRealHContext[?type1, ?type2] = ?hctx ->
  HContext(?hctx), Type(?type1), Type(?type2).
lang:constructor(`HContextFromRealHContext).
RealHContext1FromHContext[?hctx] = ?type ->
  HContext(?hctx), Type(?type).
RealHContext2FromHContext[?hctx] = ?type ->
  HContext(?hctx), Type(?type).
RealHContext1FromHContext[?hctx] = ?type1,
RealHContext2FromHContext[?hctx] = ?type2 <-
  HContextFromRealHContext[?type1, ?type2] = ?hctx.


// Merge optimization hack

// For this analysis, we only need two of the parameters that may influence the
// new context object.
MyMergeBasis(?hctx, ?value) <-
  MergeBasis(_, _, ?hctx, ?value).

// We create new context objects sparingly, because of the high cost.  We also
// cache them, so they can be looked up without a join.
Context(?calleeCtx),
ContextFromRealContext[RealHContext1FromHContext[?hctx],
                       RealHContext2FromHContext[?hctx],
                       Value:DeclaringType[?value]] = ?calleeCtx,
CachedMerge[?hctx, ?value] = ?calleeCtx <-
  MyMergeBasis(?hctx, ?value).
