//-----------------------------------------------------------------------------
// [Dynamic analysis related]
//-----------------------------------------------------------------------------


// DynamicHeapAllocation
lang:physical:storageModel[`_DynamicHeapAllocation] = "DelimitedFile".
lang:physical:filePath[`_DynamicHeapAllocation] = "facts/DynamicHeapAllocation.facts".
lang:physical:delimiter[`_DynamicHeapAllocation] = "\t".
lang:physical:hasColumnNames[`_DynamicHeapAllocation] = false.
_DynamicHeapAllocation(?param1, ?param2, ?param3, ?param4, ?param5) ->
  string(?param1),
  string(?param2),
  string(?param3),
  string(?param4),
  string(?param5).


_HeapAllocationInformation(?lineNumber, ?methodId, ?type, ?heap) <-
   Instruction:Line[?insn] = ?lineNumber,
   AssignHeapAllocation:Heap[?insn] = ?heap,
   HeapAllocation:Type[?heap] = ?type,
   Instruction:Method[?insn] = ?method,
   Method:Id[?method] = ?methodId.

+DynamicHeapAllocation(?representation, ?heap1) <-
   _MatchedHeapAllocation(?representation, ?heap1, ?order),
   !_InvalidOrder(?representation, ?order).

_InvalidOrder(?representation, ?order1) <-
   _MatchedHeapAllocation(?representation, _, ?order1),
   _MatchedHeapAllocation(?representation, _, ?order2),
   ?order2 > ?order1.

_MatchedHeapAllocation(?representation, ?heap, ?order) <-
   _HeapAllocationInformation(?lineNumber, ?inMethod, ?type, ?heap),
   _DynamicHeapAllocation(?lineNumber, ?inMethod, ?order, ?type, ?representation).

_OptDynamicHeapAllocation(?representation, ?type, ?lineNumber, ?inMethod) <-
   _DynamicHeapAllocation(?lineNumber, ?inMethod, "0", ?type, ?representation).

+UnmatchedDynamicHeapAllocation(?type, ?representation, ?lineNumber, ?inMethod) <-
   !DynamicHeapAllocation(?representation,  _), // is this working ?
   _OptDynamicHeapAllocation(?representation, ?type, ?lineNumber, ?inMethod).

+UnmatchedStaticHeapAllocation(?methodId, ?lineNumber, ?type) <-
   _HeapAllocationInformation(?lineNumber, ?methodId, ?type, _),
   !_DynamicHeapAllocation(?lineNumber, ?methodId, _, ?type, _).
