buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'commons-io:commons-io:2.4'
    }
}

apply plugin: 'antlr'
apply plugin: 'application'
apply plugin: 'groovy'

apply from  : 'benchmarks.gradle'

//applicationDefaultJvmArgs = ["-Xmx2048m"]
sourceCompatibility = "1.8"
targetCompatibility = "1.8"

group = 'org.clyze'
version = '1.0-SNAPSHOT'

//For the doop app
mainClassName = 'org.clyze.doop.Main'


task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}

repositories {
    mavenCentral()
    maven {
        url "http://centauri.di.uoa.gr:8081/artifactory/ext-snapshot-local"
    }
}

// Temporary (?) hack to allow testing with local version of a soot jar
def sootClassesJar = file('lib/sootclasses-1.1.jar')
def sootInfoflowJar = file('lib/soot-infoflow-1.1.jar')
def sootInfoflowAndroidJar = file('lib/soot-infoflow-android-1.1.jar')
[[sootClassesJar, "ext:sootclasses:1.1"],
 [sootInfoflowJar, "ext:soot-infoflow:1.1"],
 [sootInfoflowAndroidJar, "ext:soot-infoflow-android:1.1"]].each{ p->
   if (p[0].exists()) {
      dependencies {
          compile files(p[0])
      }
   }
   else {
      dependencies {
          compile p[1]
      }
   }
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.7',             // Groovy
            'commons-logging:commons-logging:1.1',              // Logging wrapper
            'log4j:log4j:1.2.14',                               // Logging implementation
            'commons-cli:commons-cli:1.2',                      // Command line processor
            'commons-io:commons-io:2.4',                        // File Utils
            'org.apache.ivy:ivy:2.3.0',                         // Apache Ivy (for downloading jars from maven repos)
            'org.ow2.asm:asm-debug-all:5.1'                     // Java Bytecode library

    // Soot is a compile time dependency
    compile 'ext:AXMLPrinter2:1.0-SNAPSHOT',
            'ext:dexlib2:2.2b4'

    // JPhantom is a runtime dependency
    runtime 'org.clyze:jphantom:1.2'
    runtime 'ext:averroes-no-properties:1.0-SNAPSHOT',
            'ext:herosclasses:1.1',
            'ext:util:2.2b4',
            'ext:axml:2.0'

    // DeepDoop
    antlr   'org.antlr:antlr4:4.5.1-1'
    compile 'org.antlr:antlr4-runtime:4.5.1-1'
    testCompile 'junit:junit:4.12',
                'org.spockframework:spock-core:1.0-groovy-2.4'
}

task generateFiles(type: JavaExec) {
    description 'Generate the doop.properties file'
    group = 'Other'
    main = 'org.clyze.doop.FileGenerator'
    classpath = sourceSets.main.runtimeClasspath
    args = [ projectDir ]
}

task bytecode2jimple(type: JavaExec) {
    description 'Generate Jimple/Shimple'
    group = 'Other'
    main = 'org.clyze.doop.soot.Main'
    classpath = sourceSets.main.runtimeClasspath
    if (!project.hasProperty('args') ||
        project.property('args').isEmpty() ||
        project.property('args').contains("-h")) {
        args = ["--bytecode2jimpleHelp"]
    } else {
        args = ["--bytecode2jimple", "--allow-phantom"] + project.property('args').tokenize()
    }
}

applicationDistribution.from(file("$projectDir/logic")) {
    into 'logic'
}

applicationDistribution.from(projectDir) {
    include 'docs', 'COLLABORATORS', 'LICENSE', 'README.md'
}


run {
    //We set the DOOP_HOME environment variable (see org.clyze.doop.Main)
    environment.DOOP_HOME = projectDir
    if (project.hasProperty('args')) {
        args project.property('args').split()
    }
}

task deepdoop(type: JavaExec) {
    description 'Run DeepDoop'
    group = 'Other'
    main = 'org.clyze.deepdoop.Main'
    classpath = sourceSets.main.runtimeClasspath
    enableAssertions = true
    if (project.hasProperty("args")) {
        args project.property("args").split()
    }
}

compileJava {
    options.compilerArgs << '-Xlint:unchecked'
}

import org.apache.commons.io.FileUtils

clean.doFirst {
    def out = System.getenv('DOOP_OUT') ?: "${projectDir}/out"
    file(out      ).list().each { f -> delete "$out/$f" }
    file('logs'   ).list().each { f -> delete "logs/$f" }
    file('results').list().each { f -> delete "results/$f" }
    FileUtils.deleteQuietly(new File('last-analysis'))
}

if (project.hasProperty('artifactory_user')) {

    apply plugin: 'maven-publish'

    //Generate a jar with the logic files
    task logicJar(type:Jar) {
        from "$projectDir/logic"
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact logicJar {
                    classifier "logic"
                }
            }
        }

        repositories {
            maven {
                credentials {
                    username artifactory_user
                    password artifactory_password
                }

                url version.endsWith("-SNAPSHOT") ?
                        "${artifactory_contextUrl}/libs-snapshot-local" :
                        "${artifactory_contextUrl}/libs-release-local"
            }
        }
    }
}
