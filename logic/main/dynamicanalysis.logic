/**
 * Dynamic analysis
 */

/*
 Default handling of context sensitivity
*/

DynamicContextToContext(?ctx, ?dctx) -> Context(?ctx), DynamicContext(?dctx).
DynamicContextToHContext(?hctx, ?dctx) -> HContext(?hctx), DynamicContext(?dctx).

#ifndef DYNAMIC_CONTEXT_SENSITIVITY
DynamicContextToContext(?ctx, ?dctx) <-
   ImmutableContext(?ctx),
   DynamicContext(?dctx).

DynamicContextToHContext(?hctx, ?dctx) <-
   DynamicContext(?dctx),
   ImmutableHContext(?hctx).
#endif


/*
 matching dynamic facts
*/


HeapAllocation:Dynamic(?heap) <- DynamicHeapAllocation(_, ?heap).

Value:byDynamicHeap[?heap] = Value:byHeap[?mergeHeap] <-
   HeapAllocation:Merge[?heap] = ?mergeHeap,
   HeapAllocation:Dynamic(?heap).

Value:byDynamicHeap[?heap] = Value:byHeap[?heap] <-
   !HeapAllocation:Merge[?heap] = _,
   HeapAllocation:Dynamic(?heap).

// Context sensitive allocations
StaticFieldPointsTo(?hctx, Value:byDynamicHeap[?heap], ?signature) <-
   DynamicStaticFieldPointsTo(?signature, ?dynamicContext, ?heap),
   DynamicContextToHContext(?hctx, ?dynamicContext).
   
InstanceFieldPointsTo(?hctx, Value:byDynamicHeap[?heap], ?fld, ?baseHctx, Value:byDynamicHeap[?baseHeap]) <-
   DynamicInstanceFieldPointsTo(?fld, ?dctx, ?heap, ?baseDCtx, ?baseHeap),
   DynamicContextToHContext(?hctx, ?dctx),
   DynamicContextToHContext(?baseHctx, ?baseDCtx).
   
ArrayIndexPointsTo(?hctx, Value:byDynamicHeap[?heap], ?baseHctx, Value:byDynamicHeap[?baseHeap]) <-
   DynamicArrayIndexPointsTo(?dctx, ?heap, ?baseDCtx, ?baseHeap),
   DynamicContextToHContext(?hctx, ?dctx),
   DynamicContextToHContext(?baseHctx, ?baseDCtx).

// Context insensitive allocations


// TODO: Consider context insensitive allocations

OptClassInitializer(?class, ?method) <- ClassInitializer[?class] = ?method.


InitializedClass(?class) <-
   DynamicReachableMethod(?method),
   OptClassInitializer(?class, ?method).

ImplicitReachable(?method) <-
   UnmatchedDynamicCallGraphEdgeNode(?method),
   DynamicReachableMethod(?method),
   !OptClassInitializer(_, ?method).

CallGraphEdge(?ctx, ?invocation, ?ctx, ?toMethod) <-
   ReachableContext(?ctx, ?method),
   !StaticMethodInvocation:Insn(?invocation),
   DynamicCallGraphEdge(?toMethod, ?invocation, ?method).


