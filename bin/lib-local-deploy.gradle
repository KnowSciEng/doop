/*
Deploy the contents of the lib directory to the ext-snapshot-local repo @ artifactory.

Each local jar has to follow the pattern:
<NAME>-<VERSION_PART>.jar
It is made available as a dependency using the pattern:
groupId: 'ext', artifactId:<NAME>, version:<VERSION_PART>
If the <VERSION_PART> doesn't exist it defaults to '1.<current time in milliseconds>'

For example, DIR_NAME/sootclasses-trunk-1.0.jar turns into ext:sootclasses-trunk:1.0.

Invoke this file using: ./gradlew -b lib-local-deploy.gradle publish -Pdir=<DIR_NAME>
If -Pdir is not provided, it defaults to the 'lib' directory located in the
directory where this script was invoked from.
*/

apply plugin: 'maven-publish'

publishing {
    publications {
        def gradleInvocationDir = System.getProperty('user.dir')
        def libDirName = project.property('dir')
        def libDir = file("${gradleInvocationDir}/${libDirName}")
        libDir.eachFile { File f ->
            def initName = f.getName() - '.jar'
            def index = initName.lastIndexOf('-')
            String namePart, versionPart
            if (index != -1) {
                namePart = initName.substring(0, index)
                versionPart = initName.substring(index+1, initName.length())
            }
            else {
                namePart = initName
                versionPart = '1.' + new Date().getTime()
            }
            create(initName, MavenPublication) {
                groupId 'ext'
                artifactId namePart
                version versionPart

                artifact f
            }
        }
    }

    repositories {
        maven {
            credentials {
                username artifactory_user
                password artifactory_password
            }

            url "${artifactory_contextUrl}/ext-snapshot-local"
        }
    }
}
