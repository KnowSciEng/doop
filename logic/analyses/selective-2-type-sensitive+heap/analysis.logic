// Context-sensitive pointer analysis with context of 2 types
// (AND for static calls 1 call-site).

ClassHasInstr(?class, ?instr) <-
  Method:DeclaringType[?method] = ?class,
  Instruction:Method[?instr] = ?method.

ClassToInstr[?class] = ?instr <-
  agg<<?instr = min(?otherInstr)>> ClassHasInstr(?class, ?otherInstr).

// In this analysis, the real context is a pair of Types AND a MethodInvocation
// (for static calls) sometimes.  The real heap context is a single Type.
// Keeping mapping functions is the way to handle analyses where HContext =
// Context (since the language considers them different types).  The type is
// not always a class type, because of arrays currently being a non-class
// subtype of Object
ContextFromRealContext[?type1, ?type2] = ?ctx ->
  Context(?ctx), Instruction(?type1), Instruction(?type2).
lang:constructor(`ContextFromRealContext).

// NOTE: Use the following when using both the normal (caller's) context and
// the invocation site for static calls.
SpecialContextFromRealContext[?type1, ?invocation, ?type2] = ?ctx ->
  Instruction(?type1), MethodInvocation(?invocation), Instruction(?type2), Context(?ctx).
lang:constructor(`SpecialContextFromRealContext).

RealContext1FromContext[?ctx] = ?type ->
  Context(?ctx), Instruction(?type).
RealContext2FromContext[?ctx] = ?type ->
  Context(?ctx), Instruction(?type).
RealContext1FromContext[?ctx] = ?type1,
RealContext2FromContext[?ctx] = ?type2 <-
  ContextFromRealContext[?type1, ?type2] = ?ctx.
// NOTE: Use the following when using both the normal (caller's) context
// and the invocation site for static calls.
RealContext1FromContext[?ctx] = ?invocation,
RealContext2FromContext[?ctx] = ?type2 <-
  SpecialContextFromRealContext[_, ?invocation, ?type2] = ?ctx.

HContextFromRealHContext[?type] = ?hctx ->
  HContext(?hctx), Instruction(?type).
lang:constructor(`HContextFromRealHContext).
RealHContextFromHContext[?hctx] = ?type ->
  HContext(?hctx), Instruction(?type).
RealHContextFromHContext[?hctx] = ?type <-
  HContextFromRealHContext[?type] = ?hctx.


// Merge optimization hack

// For this analysis, we only need two of the parameters that may influence the
// new context object.
MyMergeBasis(?hctx, ?value) <-
  MergeBasis(_, _, ?hctx, ?value).

// We create new context objects sparingly, because of the high cost.  We also
// cache them, so they can be looked up without a join.
Context(?calleeCtx),
ContextFromRealContext[RealHContextFromHContext[?hctx], ClassToInstr[Value:DeclaringType[?value]]] = ?calleeCtx,
CachedMerge[?hctx, ?value] = ?calleeCtx <-
  MyMergeBasis(?hctx, ?value).
