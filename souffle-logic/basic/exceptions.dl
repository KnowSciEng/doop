/**
 * (REVIEW-Yannis) Note how this logic is superlinear. We keep relations that
 * link every exception handler to every relevant (i.e., throw or methcall)
 * instruction under its range, and to every type that the exception handler
 * can handle, including all subtypes of the declared type.  It is not easy to
 * change this, nor perhaps too valuable. But it is certainly a spot where
 * bottom-up evaluation with an explicit representation hurts us.  We have very
 * large ExceptionHandler:At, PossibleExceptionHandler, etc. relations.
 * NOTE: currently exception objects are allocated context-insensitively.
/**
 * An exception of a specific type, thrown at an instruction, is handled by an
 * exception handler.
 */
ExceptionHandler_At(?type, ?instruction, ?handler) :-
    PossibleExceptionHandler(?handler, ?type, ?instruction),
    !ImpossibleExceptionHandler(?handler, ?type, ?instruction).

/**
 * An exception type that is caught by an earlier exception handler (not
 * ?handler).
 */
ImpossibleExceptionHandler(?handler, ?type, ?instruction) :-
    PossibleExceptionHandler(?handler, ?type, ?instruction),
    ExceptionHandler_Before(?previous, ?handler),
    PossibleExceptionHandler(?previous, ?type, ?instruction).

PossibleExceptionHandler(?handler, ?type, ?instruction) :-
    ExceptionHandler_InRange(?handler, ?instruction),
    ExceptionHandler_Type(?handler, ?type).

PossibleExceptionHandler(?handler, ?subtype, ?instruction) :-
    ExceptionHandler_InRange(?handler, ?instruction),
    ExceptionHandler_Type(?handler, ?type),
    Superclass(?subtype, ?type).


ExceptionHandler_InRange(?handler, ?instruction) :-
    Instruction_Method(?instruction, ?method),
    ExceptionHandler_Method(?handler, ?method), // TODO: this could be optimized
    Instruction_Index(?instruction, ?index),
    ExceptionHandler_Begin(?handler, ?begin),
    ?begin <= ?index,
    ExceptionHandler_End(?handler, ?end),
    ?index < ?end.

ExceptionHandler_Before(?previous, ?handler) :-
    ExceptionHandler_Previous(?handler, ?previous).

ExceptionHandler_Before(?before, ?handler) :-
    ExceptionHandler_Before(?middle, ?handler),
    ExceptionHandler_Previous(?middle, ?before).

InRangeOfExceptionHandler(?instruction) :-
    ExceptionHandler_InRange(_, ?instruction).
