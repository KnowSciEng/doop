/**
 * Elegance hacks, using macros: instead of burdening all normal
 * analyses, provide default definitions for the common cases.
 */

#ifndef MULTIPLE_SEGREGATED_ANALYSES
ImmutableContextFromHContext(?hctx, ?ctx) :-
  isImmutableContext(?ctx),
  isHContext(?hctx).

ImmutableHContextFromContext(?ctx, ?hctx) :-
  isImmutableHContext(?hctx),
  isContext(?ctx).

ImmutableContextFromContext(?ctx1, ?ctx2) :-
  isImmutableContext(?ctx2),
  isContext(?ctx1).
#endif // MULTIPLE_SEGREGATED_ANALYSES

#ifndef CompatibleContextAndHContextMacro
#define CompatibleContextAndHContextMacro(ctx, hctx) ctx = ctx, hctx = hctx
#endif
#ifndef CompatibleHContextsMacro
#define CompatibleHContextsMacro(hctx1, hctx2) hctx1 = hctx1, hctx2 = hctx2
#endif

#include "init.dl"

/**
 * Generic context-sensitive pointer analysis
 */


/**
 * Heap allocation
 */

configuration.RecordContextRequest(?ctx, ?heapValue, ?var) :-
  AssignNormalHeapAllocation(?heapValue, ?var, ?inmethod),
  ReachableContext(?ctx, ?inmethod).

VarPointsTo(?hctx, ?value, ?ctx, ?var) :-
  configuration.RecordContextRequest(?ctx, ?value, ?var),
  configuration.RecordContextResponse(?ctx, ?value, ?var, ?hctx),
  ObjectShouldNotBeRefined(?value).

configuration.RecordContextRequest(?ctx, ?heapValue, ?var) :-
  AssignAuxiliaryHeapAllocation(?heapValue, ?var, ?inmethod),
  ReachableContext(?ctx, ?inmethod).

isHContext(?newHCtx),
VarPointsTo(?newHCtx, ?heapValue, ?ctx, ?var) :-
  configuration.RecordContextRequest(?ctx, ?heapValue, ?var),
  configuration.RecordContextResponse(?ctx, ?heapValue, ?var, ?newHCtx).

#ifdef RecordRefinedMacro
isHContext(?newHCtx),
VarPointsTo(?newHCtx, ?heapValue, ?ctx, ?var) :-
  AssignNormalHeapAllocation(?heapValue, ?var, ?inmethod),
  ReachableContext(?ctx, ?inmethod),
  ObjectShouldBeRefined(?heapValue),
  RecordRefinedMacro(?ctx, ?heapValue, ?newHCtx).
#endif

VarPointsTo(?hctx, ?heapValue, ?ctx, ?var) :-
  Reachable(?inmethod),  // optimization purposes
  AssignContextInsensitiveHeapAllocation(?heapValue, ?var, ?inmethod),
  ReachableContext(?ctx, ?inmethod),
  ImmutableHContextFromContext(?ctx, ?hctx).
  .plan 1:(4,3,2,1)
  .plan 2:(4,3,1,2)

/**
 * Null assignments
 */
VarPointsTo(?hctx, ?null, ?toCtx, ?to) :-
   Reachable(?inmethod),  // optimization purposes
   AssignNull(?to, ?inmethod),
   Value_Null(?null),
   ReachableContext(?toCtx, ?inmethod),
   ImmutableHContextFromContext(?toCtx, ?hctx).
   .plan 2:(5,4,1,2,3)

/**
 * Assignment of numeric constant. Currently context-insensitive and
 * only used for android analyses, hence the ad hoc logic to match
 * a specific host class (R).
 */

VarPointsTo(?hctx, ?value, ?toCtx, ?to) :-
   ReachableContext(?toCtx, ?inmethod),
   Instruction_Method(?insn, ?inmethod),
   (AssignNumConstant_Id(?insn, ?const);
    AssignCastNumConstant_Id(?insn, ?const)), // TODO: check the cast?
   AssignInstruction_To(?insn, ?to),
   Value_Num(?value, ?const),
   ImmutableHContextFromContext(?toCtx, ?hctx).
   .plan 1:(6,1,2,3,4,5)

// No need to check if type compatible: check is done at original
// inputs to VarPointsTo
VarPointsTo(?hctx, ?value, ?ctx, ?to) :-
  VarPointsTo(?hctx, ?value, ?ctx, ?from),
  AssignLocal(?from, ?to, _).

/**
 * Cast assignments
 */

.decl OptAssignCast(?type:Type, ?to:Var, ?from:Var)

OptAssignCast(?type, ?to, ?from) :-
  Reachable(?inmethod),
  AssignCast(?type, ?from, ?to, ?inmethod).

VarPointsTo(?hctx, ?value, ?ctx, ?to) :-
  OptAssignCast(?type, ?to, ?from),
  VarPointsTo(?hctx, ?value, ?ctx, ?from),
  Value_Type(?value, ?valuetype),
  basic.SupertypeOf(?type, ?valuetype).
#ifdef INFORMATION_FLOW
  .plan 2:(3,4,2,1)
#endif

/**
 * Load instance fields
 */

LoadHeapInstanceField(?ctx, ?to, ?sig, ?basehctx, ?basevalue) :-
  LoadInstanceField(?base, ?sig, ?to, _),
  VarPointsTo(?basehctx, ?basevalue, ?ctx, ?base).


#ifndef FEATHERWEIGHT_ANALYSIS
//// Much slower in Souffle, for large analyses
// VarPointsTo(?hctx, ?value, ?ctx, ?to) :-
//   LoadHeapInstanceField(?ctx, ?to, ?signature, ?basehctx, ?basevalue),
//   InstanceFieldPointsTo(?hctx, ?value, ?signature, ?basehctx, ?basevalue).
//    .plan 1:(2,1)

.decl HeapInterproceduralAssign(?toCtx:Context, ?to:Var, ?fromCtx:Context, ?from:Var)
HeapInterproceduralAssign(?ctxTo, ?to, ?ctxFrom, ?from) :-
  LoadHeapInstanceField(?ctxTo, ?to, ?signature, ?basehctx, ?basevalue),
  StoreHeapInstanceField(?signature, ?basehctx, ?basevalue, ?ctxFrom, ?from),
  !Value_Null(?basevalue).
  .plan 1:(2,1)

VarPointsTo(?hctx, ?value, ?toCtx, ?to) :-
  HeapInterproceduralAssign(?toCtx, ?to, ?fromCtx, ?from),
  VarPointsTo(?hctx, ?value, ?fromCtx, ?from),
  NotSpecialObject(?value).
  .plan 1:(2,1,3)

#else
// FEATHERWEIGHT_ANALYSIS
VarPointsTo(?hctx, ?value, ?ctx, ?to) :-
  LoadHeapInstanceField(?ctx, ?to, ?signature, ?basehctx, ?basevalue),
  InstanceFieldPointsTo(?hctx, ?value, ?signature, ?basehctx, ?basevalue).
#endif

/**
 * Store instance fields
 */
#ifndef FEATHERWEIGHT_ANALYSIS
InstanceFieldPointsTo(?hctx, ?value, ?signature, ?basehctx, ?basevalue) :-
  StoreHeapInstanceField(?signature, ?basehctx, ?basevalue, ?ctx, ?from),
  VarPointsTo(?hctx, ?value, ?ctx, ?from),
  NotSpecialObject(?value),
  !Value_Null(?basevalue).
.plan 1:(2,1,3)
#else
.decl NoInformationOnSignature(?signature: Field)
.output NoInformationOnSignature

NoInformationOnSignature(?signature) :-
 isField(?signature),
 !DynamicInstanceFieldPointsTo(?signature, _, _, _, _).

.decl ConstructorVar(?var:Var)

ConstructorVar(?var) :-
  Var_DeclaringMethod(?var, ?meth),
  ApplicationMethod(?meth),
  ClassConstructor(?meth, _).

InstanceFieldPointsTo(?hctx, ?value, ?signature, ?basehctx, ?basevalue) :-
  StoreHeapInstanceField(?signature, ?basehctx, ?basevalue, ?ctx, ?from),
  ConstructorVar(?from),
  VarPointsTo(?hctx, ?value, ?ctx, ?from),
  NoInformationOnSignature(?signature),
  Value_byDynamicHeap(_, ?value),
  NotSpecialObject(?value),
  !Value_Null(?basevalue).
.plan 1:(3,2,1,4,5,6)
#endif


#if defined(FEATHERWEIGHT_ANALYSIS) && defined(INFORMATION_FLOW)
InstanceFieldPointsTo(?hctx, ?value, ?signature, ?basehctx, ?basevalue) :-
  StoreHeapInstanceField(?signature, ?basehctx, ?basevalue, ?ctx, ?from),
  TaintedVarPointsTo(?value, ?ctx, ?from),
  isImmutableHContext(?hctx),
  !Value_Null(?basevalue).
  .plan 1:(2,1,3)
#endif

StoreHeapInstanceField(?signature, ?basehctx, ?basevalue, ?ctx, ?from) :-
  StoreInstanceField(?from, ?base, ?signature, _),
  VarPointsTo(?basehctx, ?basevalue, ?ctx, ?base).

/**
 * Load static fields
 */

VarPointsTo(?hctx, ?value, ?ctx, ?to) :-
  StaticFieldPointsTo(?hctx, ?value, ?sig),
  LoadStaticField(?sig, ?to, ?inmethod),
  ReachableContext(?ctx, ?inmethod),
  CompatibleContextAndHContextMacro(?ctx, ?hctx).
  .plan 1:(3,2,1)

/**
 * Store static fields
 */
#ifndef FEATHERWEIGHT_ANALYSIS
StaticFieldPointsTo(?hctx, ?value, ?signature) :-
  ReachableStoreStaticFieldFrom(?from),
  StoreStaticField(?from, ?signature, _),
  VarPointsTo(?hctx, ?value, _, ?from).
#endif
#if defined(FEATHERWEIGHT_ANALYSIS) && defined(INFORMATION_FLOW)
StaticFieldPointsTo(?hctx, ?value, ?signature) :-
  ReachableStoreStaticFieldFrom(?from),
  StoreStaticField(?from, ?signature, _),
  TaintedVarPointsTo(?value, _, ?from),
  isImmutableHContext(?hctx).
#endif

.decl ReachableStoreStaticFieldFrom(?from:Var)

ReachableStoreStaticFieldFrom(?from) :-
  Reachable(?inmethod),
  StoreStaticField(?from, _, ?inmethod).

/**
 * Store default value in static field (android-only!)
 *
 */
#ifdef ANDROID

StaticFieldPointsTo(?hctx, ?value, ?signature) :-
  FieldInitialValue(?signature, ?const),
  Value_Num(?value, ?const),
  isImmutableHContext(?hctx).
#endif

/**
 * Load array index
 */

// Intermediate relations used for optimization purposes
.decl OptLoadHeapArrayIndex(?var:Var, ?value:Value)
.decl Temp1(?value:Value, ?var:Var)
.decl Temp2(?ctx:Context, ?var:Var, ?hctx:HContext, ?value:Value)

OptLoadHeapArrayIndex(?to, ?basevalue) :-
  LoadHeapArrayIndex(_, ?to, _, ?basevalue).

Temp1(?basevalue, ?to) :-
  NotEmptyArrayValue(?basevalue),
  OptLoadHeapArrayIndex(?to, ?basevalue),
  Value_Type(?basevalue, ?basevaluetype),
  ComponentType(?basevaluetype, ?basecomponenttype),
  Var_Type(?to, ?type),
  basic.SupertypeOf(?type, ?basecomponenttype).

Temp2(?ctx, ?to, ?basehctx, ?basevalue) :-
  Temp1(?basevalue, ?to),
  LoadHeapArrayIndex(?ctx, ?to, ?basehctx, ?basevalue).

VarPointsTo(?hctx, ?value, ?ctx, ?to) :-
  Temp2(?ctx, ?to, ?basehctx, ?basevalue),
  ArrayIndexPointsTo(?hctx, ?value, ?basehctx, ?basevalue).

// The above logic aims to optimize the following rule:
//
// VarPointsTo(?hctx, ?value, ?ctx, ?to) :-
//   NotEmptyArrayValue(?basevalue),
//   LoadHeapArrayIndex(?ctx, ?to, ?basehctx, ?basevalue),
//   ArrayIndexPointsTo(?hctx, ?value, ?basehctx, ?basevalue).
//   // this is necessary to handle untyped array operations, e.g., the
//   // simulation of arrayCopy calls as Load/Stores
//   Value_Type(?basevalue, ?basevaluetype),
//   Var_Type(?to, ?type),
//   ComponentType(?basevaluetype, ?basecomponenttype),
//   basic.SupertypeOf(?type, ?basecomponenttype).

.decl NotEmptyArrayValue(?value:Value)

NotEmptyArrayValue(?heapValue) :-
  Value_isHeap(?heapValue),
  !HeapAllocation_EmptyArray(?heapValue).
NotEmptyArrayValue(?value) :-
  Value_isNonHeap(?value).

LoadHeapArrayIndex(?ctx, ?to, ?basehctx, ?basevalue) :-
  ReachableLoadArrayIndexBase(?base),
  OptLoadArrayIndex(?to, ?base),
  VarPointsTo(?basehctx, ?basevalue, ?ctx, ?base).

OptLoadArrayIndex(?to, ?base) :-
  LoadArrayIndex(?base, ?to, _).

.decl ReachableLoadArrayIndexBase(?base:Var)

ReachableLoadArrayIndexBase(?base) :-
  Reachable(?inmethod),
  LoadArrayIndex(?base, _, ?inmethod).

/**
 * Store array index
 */

#ifndef FEATHERWEIGHT_ANALYSIS
OptStoreIntoArray(?hctx, ?basehctx, ?basevalue, ?value) :-
  StoreHeapArrayIndex(?basehctx, ?basevalue, ?ctx, ?from),
  VarPointsTo(?hctx, ?value, ?ctx, ?from).
#endif
#if defined(FEATHERWEIGHT_ANALYSIS) && defined(INFORMATION_FLOW)
OptStoreIntoArray(?hctx, ?basehctx, ?basevalue, ?value) :-
  StoreHeapArrayIndex(?basehctx, ?basevalue, ?ctx, ?from),
  TaintedVarPointsTo(?value, ?ctx, ?from),
  isImmutableHContext(?hctx).
#endif

ArrayIndexPointsTo(?hctx, ?value, ?basehctx, ?basevalue) :-
  OptStoreIntoArray(?hctx, ?basehctx, ?basevalue, ?value),
  NotSpecialObject(?value),
  NotEmptyArrayValue(?basevalue),
  Value_Type(?value, ?valuetype),
  Value_Type(?basevalue, ?basevaluetype),
  ComponentType(?basevaluetype, ?componenttype),
  basic.SupertypeOf(?componenttype, ?valuetype).

StoreHeapArrayIndex(?basehctx, ?basevalue, ?ctx, ?from) :-
  ReachableStoreArrayIndexBase(?base),
  OptStoreArrayIndex(?from, ?base),
  VarPointsTo(?basehctx, ?basevalue, ?ctx, ?base).

.decl ReachableStoreArrayIndexBase(?base:Var)

ReachableStoreArrayIndexBase(?base) :-
  Reachable(?inmethod),
  StoreArrayIndex(_, ?base, ?inmethod).

OptStoreArrayIndex(?from, ?base) :-
  StoreArrayIndex(?from, ?base, _).


/**
 * Assignments for method invocations
 */

// This logic replaces (optimizes) general-purpose inter-procedural
// assignments for args and returns. It should be unnecessary, but
// Souffle appreciates smaller deltas, since plans cannot change
// per-rule-evaluation, only per-rule-structure.

.decl OptArgAssign(?toCtx:Context, ?to:Var, ?fromCtx:Context, ?from:Var)
.decl OptReturnAssign(?toCtx:Context, ?to:Var, ?fromCtx:Context, ?from:Var)
.decl OptArgVarPointsTo(?hctx:HContext, ?value:Value, ?ctx:Context, ?var:Var)
.decl OptRetVarPointsTo(?hctx:HContext, ?value:Value, ?ctx:Context, ?var:Var)

OptArgAssign(?calleeCtx, ?formal, ?callerCtx, ?actual) :-
  CallGraphEdge(?callerCtx, ?invocation, ?calleeCtx, ?method),
  ActualParam(?index, ?invocation, ?actual),
  FormalParam(?index, ?method, ?formal).

OptReturnAssign(?callerCtx, ?local, ?calleeCtx, ?return) :-
  CallGraphEdge(?callerCtx, ?invocation, ?calleeCtx, ?method),
  AssignReturnValue(?invocation, ?local),
  ReturnVar(?return, ?method).

OptArgVarPointsTo(?hctx, ?value, ?actualCtx, ?actual) :-
  ActualParam(_, _, ?actual),
  VarPointsTo(?hctx, ?value, ?actualCtx, ?actual).

OptRetVarPointsTo(?hctx, ?value, ?returnCtx, ?return) :-
  ReturnVar(?return, _),
  VarPointsTo(?hctx, ?value, ?returnCtx, ?return).

VarPointsTo(?hctx, ?value, ?toCtx, ?to) :-
  OptArgVarPointsTo(?hctx, ?value, ?fromCtx, ?from),
  OptArgAssign(?toCtx, ?to, ?fromCtx, ?from).
  .plan 1:(2,1)

VarPointsTo(?hctx, ?value, ?toCtx, ?to) :-
  OptRetVarPointsTo(?hctx, ?value, ?fromCtx, ?from),
  OptReturnAssign(?toCtx, ?to, ?fromCtx, ?from).
  .plan 1:(2,1)


/**
 * Catch-all Assignments. Use sparingly!
 */

// No longer using general-purpose inter-procedural assignments for lots
// of different flows. Souffle cannot optimize well. Best to break up
// key cases (like arg-return flow, above) into separate, well-optimized rules.
VarPointsTo(?hctx, ?value, ?toCtx, ?to) :-
  OptInterproceduralAssign(?toCtx, ?to, ?fromCtx, ?from),
  VarPointsTo(?hctx, ?value, ?fromCtx, ?from).
//  .plan 1:(2,1)
//// no plan for now. These kinds of inter-proc assignments should be very few!


/**
 * Virtual Method Invocation
 */

.decl OptVirtualMethodInvocationBase(?invocation:MethodInvocation, ?base:Var)

OptVirtualMethodInvocationBase(?invocation, ?base) :-
  Reachable(?inmethod),
  Instruction_Method(?invocation, ?inmethod),
  VirtualMethodInvocation_Base(?invocation, ?base).

// The optimization/advanced merge treatment is as follows: the main
// analysis (this file) first creates all the possible bindings that
// the Merge logic might need to create a new context. (It uses
// ContextRequest to communicate these bindings to the individual
// context-sensitive analysis.) Then each individual analysis creates
// new context objects by employing arbitrary logic, and communicates
// back to the main analysis the results of context creation using
// ContextResponse.
configuration.ContextRequest(?callerCtx, ?hctx, ?invocation, ?value) :-
  OptVirtualMethodInvocationBase(?invocation, ?base),
  VarPointsTo(?hctx, ?value, ?callerCtx, ?base).

// The main analysis is merely looking up the previously created
// context objects.
VarPointsTo(?hctx, ?value, ?calleeCtx, ?this) :-
  configuration.ContextRequest(?callerCtx, ?hctx, ?invocation, ?value),
  configuration.ContextResponse(?callerCtx, ?hctx, ?invocation, ?value, ?calleeCtx),
  Value_Type(?value, ?valuetype),
  basic.ResolveInvocation(?valuetype, ?invocation, ?tomethod),
  ThisVar(?tomethod, ?this),
  SiteShouldNotBeRefined(?invocation).
  .plan 1:(2,1,3,4,5,6)


#ifdef MergeRefinedMacro
#ifndef CachedMergeRefinedMacro
// Default behavior
/**
 * This logic applies to refinement-based (or "adaptive") analyses. The
 * analysis is first run with the filter predicate (SiteToRefine) empty,
 * performing a low-precision but cheap computation. Then the external logic
 * runs delta rules to populate the filter predicate and re-runs the analysis.
 * (Note that the #ifdef just checks if the analysis has defined the
 * appropriate macro. This is not an input flag.)
 */
VarPointsTo(?hctx, ?value, ?newCtx, ?this) :-
  VarPointsTo(?hctx, ?value, ?callerCtx, ?base),
  OptVirtualMethodInvocationBase(?invocation, ?base),
  Value_Type(?value, ?valuetype),
  basic.ResolveInvocation(?valuetype, ?invocation, ?tomethod),
  ThisVar(?tomethod, ?this),
  SiteShouldBeRefined(?invocation),
  MergeRefinedMacro(?callerCtx, ?invocation, ?hctx, ?value, ?calleeCtx).

#else /* there is optimized behavior available */

configuration.ContextRequest(?callerCtx, ?hctx, ?invocation, ?value) :-
  OptVirtualMethodInvocationBase(?invocation, ?base),
  VarPointsTo(?hctx, ?value, ?callerCtx, ?base).

VarPointsTo(?hctx, ?value, ?calleeCtx, ?this) :-
  configuration.ContextRequest(?callerCtx, ?hctx, ?invocation, ?value),
  CachedMergeRefinedMacro(?callerCtx, ?invocation, ?hctx, ?value, ?calleeCtx),
  Value_Type(?value, ?valuetype),
  basic.ResolveInvocation(?valuetype, ?invocation, ?tomethod),
  ThisVar(?tomethod, ?this),
  SiteShouldBeRefined(?invocation).
  .plan 1:(2,1,3,4,5,6)
#endif /* #ifndef CachedMergeRefinedMacro */
#endif /* #ifdef MergeRefinedMacro */

/**
 * Special method invocations
 */

.decl OptSpecialMethodInvocationBase(?invocation:MethodInvocation, ?base:Var)

OptSpecialMethodInvocationBase(?invocation, ?base) :-
  Reachable(?inmethod),
  Instruction_Method(?invocation, ?inmethod),
  SpecialMethodInvocation_Base(?invocation, ?base).

configuration.ContextRequest(?callerCtx, ?hctx, ?invocation, ?value) :-
  OptSpecialMethodInvocationBase(?invocation, ?base),
  VarPointsTo(?hctx, ?value, ?callerCtx, ?base).

VarPointsTo(?hctx, ?value, ?calleeCtx, ?this) :-
  configuration.ContextRequest(?callerCtx, ?hctx, ?invocation, ?value),
  configuration.ContextResponse(?callerCtx, ?hctx, ?invocation, ?value, ?calleeCtx),
  MethodInvocation_Method(?invocation, ?tomethod),
  ThisVar(?tomethod, ?this),
  isSpecialMethodInvocation_Insn(?invocation),
  SiteShouldNotBeRefined(?invocation).
  //.plan 1:(2,1,3,4,5,6)

#ifdef MergeRefinedMacro
#ifndef CachedMergeRefinedMacro
// Default behavior
VarPointsTo(?hctx, ?value, ?newCtx, ?this) :-
  VarPointsTo(?hctx, ?value, ?callerCtx, ?base),
  OptSpecialMethodInvocationBase(?invocation, ?base),
  MethodInvocation_Method(?invocation, ?tomethod),
  isSpecialMethodInvocation_Insn(?invocation),
  ThisVar(?tomethod, ?this),
  SiteShouldBeRefined(?invocation),
  MergeRefinedMacro(?callerCtx, ?invocation, ?hctx, ?value, ?newCtx).

#else /* there is advanced behavior available */

configuration.ContextRequest(?callerCtx, ?hctx, ?invocation, ?value) :-
  OptSpecialMethodInvocationBase(?invocation, ?base),
  VarPointsTo(?hctx, ?value, ?callerCtx, ?base).

VarPointsTo(?hctx, ?value, ?calleeCtx, ?this) :-
  configuration.ContextRequest(?callerCtx, ?hctx, ?invocation, ?value),
  OptimizeMergeRefinedMacro(?callerCtx, ?invocation, ?hctx, ?value, ?calleeCtx),
  MethodInvocation_Method(?invocation, ?tomethod),
  isSpecialMethodInvocation_Insn(?invocation),
  ThisVar(?tomethod, ?this),
  SiteShouldBeRefined(?invocation).
#endif /* #ifndef CachedMergeRefinedMacro */
#endif /* #ifdef MergeRefinedMacro


/**
 * Reachable
 */
ReachableContext(?ctx, ?method) :-
  CallGraphEdge(_, _, ?ctx, ?method).

Reachable(?method) :-
  ReachableContext(_, ?method).

/**
 * Logic to decide whether to apply refined or regular bindings for
 * methods and objects
 */
// We want to allow predicates that express the *complement* of the set
// of objects to refine. We introduce derived-only temp predicates to
// avoid logic replication in the points-to rule itself.

.decl ObjectShouldNotBeRefined(?heap:HeapAllocation)

ObjectShouldNotBeRefined(?heap) :-
  NegativeObjectFilter("true"),
  ValueToRefine(?heap).

ObjectShouldNotBeRefined(?heap) :-
  !NegativeObjectFilter("true"),
  isValue(?heap),
  !ValueToRefine(?heap).

.decl ObjectShouldBeRefined(?heap:HeapAllocation)

ObjectShouldBeRefined(?heap) :-
  !NegativeObjectFilter("true"),
  ValueToRefine(?heap).

ObjectShouldBeRefined(?heap) :-
  NegativeObjectFilter("true"),
  isValue(?heap),
  !ValueToRefine(?heap).

.decl SiteShouldNotBeRefined(?invocation:MethodInvocation)

SiteShouldNotBeRefined(?invocation) :-
  !NegativeSiteFilter("true"),
  isMethodInvocation(?invocation),
  !SiteToRefine(?invocation).

SiteShouldNotBeRefined(?invocation) :-
  NegativeSiteFilter("true"),
  SiteToRefine(?invocation).

.decl SiteShouldBeRefined(?invocation:MethodInvocation)

SiteShouldBeRefined(?invocation) :-
  !NegativeSiteFilter("true"),
  SiteToRefine(?invocation).

SiteShouldBeRefined(?invocation) :-
  NegativeSiteFilter("true"),
  isMethodInvocation(?invocation),
  !SiteToRefine(?invocation).
