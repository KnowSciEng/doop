/*************************************************************
 * java.lang.Object.getClass
 *
 *   public final Class<?> getClass();
 *
 * The actual result type is Class<? extends |X|> where |X| is the erasure of
 * the static type of the expression on which getClass is called.
 *
 * Although this is a reflection operation, it is easy to analyse statically,
 * because we know the type of every heap allocation.
 *************************************************************/

/**
 * Evaluate Object.getClass
 */
VarPointsTo(?hctx, ?classValue, ?ctx, ?to) <-
  ImmutableHContextFromContext[?ctx] = ?hctx,
  java:lang:Object:getClass(?to, ?from),
  VarPointsTo(_, ?value, ?ctx, ?from),
  Value:Type[?value] = ?type,
  ReifiedClass[?type] = ?classValue.

/*************************************************************
 * java.lang.Class.forName
 *
 *   public static Class<?> forName(String className)
 *   public static Class<?> forName(String name, boolean initialize, ClassLoader loader)
 *
 * Class.forName is the main fragile point of a static pointer analysis.
 * Arbitrary strings can be constructed by the program and used as an argument
 * to Class.forName, so we cannot predict which classes they correspond to.
 *
 * Therefore, this is also the main user-configuration point (some other
 * points-to analysis implementations configure newInstance, but we think
 * Class.forName is the appropriate place)
 *
 * We do automatically support class name strings that occur as constants in
 * the program. For those, configuration is not necessary.
 *************************************************************/

/**
 * Class.forName invocations with string constant parameters
 */
ForwardReflectiveAssignClassConstant(?ctx, ?invocation, ?type),
ForwardReflectiveAssignClassConstantString(?ctx, ?invocation, ?type, ?constantHeap) <-
  java:lang:Class:forName(?invocation, ?inmethod),
  ReachableContext(?ctx, ?inmethod),
  ActualParam[0, ?invocation] = ?param,
  VarPointsTo(_, ?constant, ?ctx, ?param),
  Value:Heap[?constant] = ?constantHeap,
  ClassNameMatchingStringConstant:Type(?type, ?constantHeap).

// If it is a local and precise string, consider it a high-confidence inference
ForwardHighConfidenceReflectiveAssignClassConstant(?ctx, ?invocation, ?type) <-
  ForwardReflectiveAssignClassConstantString(?ctx, ?invocation, ?type, ?mergeHeap),
  Instruction:Method[?invocation] = ?inmethod,
  AssignHeapInMethod(?constantHeap, ?inmethod, ?mergeHeap),
  (NumberOfClassesMatchingConstant[?constantHeap] < StringPrecisionThreshold[];
   ClassNameMatchingStringConstant:Type(?type, ?constantHeap)).


/*************************************************************
 * java.lang.Class.getName
 *
 *   public String getName()
 *************************************************************/
VarPointsTo(?hctx, ?classNameValue, ?callerCtx, ?to) <-
  ImmutableHContextFromContext[?callerCtx] = ?hctx,
  java:lang:Class:getName(?to, ?from),
  VarPointsTo(_, ?classValue, ?callerCtx, ?from),
  OptReifiedClass(?instanceType, ?classValue),
  Type:Id(?instanceType:?className),
  HeapAllocation:byId[?className] = ?classNameHeap,
  StringConstant(?classNameHeap),
  Value:byHeap[?classNameHeap] = ?classNameValue.

// REVIEW: Note how unsound this rule is: it returns a string only
// if the string exists in the input. 

// REVIEW: when getName returns a string, but our normal reflection
// logic would have merged that string with others, should the result
// be the string itself or its HeapAllocation:Merge?

/*************************************************************
 * java.lang.Class.getComponentType
 *
 *   public Class<?> getComponentType()
 *************************************************************/
VarPointsTo(?hctx, ?componentClassValue, ?callerCtx, ?to) <-
  ImmutableHContextFromContext[?callerCtx] = ?hctx,
  java:lang:Class:getComponentType(?to, ?from),
  VarPointsTo(_, ?classValue, ?callerCtx, ?from),
  ReifiedClass[?type] = ?classValue,
  ArrayType(?type), ComponentType[?type] = ?componentType,
  ReifiedClass[?componentType] = ?componentClassValue.


/*************************************************************
 * java.lang.Class.getSuperclass
 *
 *   public Class<? super T> getSuperclass()
 *
 * If the base type is java.lang.Object, an interface or a primitive type, then
 * null is returned.
 *
 * TODO should (can?) we handle null (for NullPointerException)?
 *************************************************************/
VarPointsTo(?hctx, ?superClassValue, ?callerCtx, ?to) <-
  ImmutableHContextFromContext[?callerCtx] = ?hctx,
  java:lang:Class:getSuperclass(?to, ?from),
  VarPointsTo(_, ?classValue, ?callerCtx, ?from),
  ReifiedClass[?type] = ?classValue,
  ClassType(?type), DirectSuperclass[?type] = ?superType,
  ReifiedClass[?superType] = ?superClassValue.

VarPointsTo(?hctx, ?superClassValue, ?callerCtx, ?to) <-
  ImmutableHContextFromContext[?callerCtx] = ?hctx,
  java:lang:Class:getSuperclass(?to, ?from),
  VarPointsTo(_, ?classValue, ?callerCtx, ?from),
  ReifiedClass[?type] = ?classValue,
  ArrayType(?type), Type:Id(?superType:"java.lang.Object"),
  ReifiedClass[?superType] = ?superClassValue.


/*************************************************************
 * java.lang.Class.getInterfaces
 *
 *   public Class<?>[] getInterfaces()
 *************************************************************/

ArrayIndexPointsTo(?hctx, ?superInterfaceValue, ?hctx, ?arrayValue) <-
  FillArray:Interfaces(?hctx, ?arrayValue, ?type),
  DirectSuperinterface(?type, ?superInterface),
  ReifiedClass[?superInterface] = ?superInterfaceValue.

_OptReifiedClass(?type, ?classValue) <-
  ReifiedClass[?type] = ?classValue.

_OptReflectiveAllocation(?arrayValue, ?invocation) <-
  ReflectiveAllocation[?invocation, _] = ?arrayValue.

FillArray:Interfaces(?hctx, ?arrayValue, ?type),
VarPointsTo(?hctx, ?arrayValue, ?callerCtx, ?to) <-
  ImmutableHContextFromContext[?callerCtx] = ?hctx,
  java:lang:Class:getInterfaces(?invocation, ?to, ?from),
  VarPointsTo(_, ?classValue, ?callerCtx, ?from),
  _OptReifiedClass(?type, ?classValue),
  _OptReflectiveAllocation(?arrayValue, ?invocation).


/*************************************************************
 * java.lang.Class.getDeclaredMethods and variants
 *
 *   public Method[] getMethods()
 *   public Method getMethod(String name, Class[] parameterTypes)
 *   public Method[] getDeclaredMethods()
 *   public Method getDeclaredMethod(String name, Class[] parameterTypes)
 *************************************************************/

/*
//VERSION-A
ArrayIndexPointsTo(?hctx, ?value, ?hctx, ?arrayValue) <-
  FillArray:PublicMethods(?hctx, ?arrayValue, ?type),
  Method:DeclaringType[?signature] = ?type,
  Method:Modifier("public", ?signature),
  ReifiedMethod[?signature] = ?value,
  MethodNameMatchingStringConstant:Signature(?signature, _).
// we assume the returned methods will be filtered by some string
*/

FillArray:PublicMethods(?hctx, ?arrayValue, ?type),
VarPointsTo(?hctx, ?arrayValue, ?ctx, ?to) <-
  java:lang:Class:getMethods(?invocation, ?to, ?from),
  VarPointsTo(_, ?classValue, ?ctx, ?from),
  _OptReifiedClass(?type, ?classValue),
  _OptReflectiveAllocation(?arrayValue, ?invocation),
  ImmutableHContextFromContext[?ctx] = ?hctx.

// NOTE that this won't fire when we are in DISTINGUISH_NO_STRING_CONSTANTS
// mode. The constant will never flow to ?param.
ForwardReflectiveAssignMethodConstant(?ctx, ?to, ?signature),
ForwardReflectiveAssignMethodConstantString(?ctx, ?to, ?signature, ?constantHeap) <-
  java:lang:Class:getMethod(?to, ?param, ?from),
  VarPointsTo(_, ReifiedClass[?type], ?ctx, ?from),
  VarPointsTo(_, ?constant, ?ctx, ?param),
  Value:Heap[?constant] = ?constantHeap,
  MethodNameMatchingStringConstant:Signature(?signature, ?constantHeap),
  SupertypeOf(?superClass, ?type),
  Method:DeclaringType[?signature] = ?superClass,
  Method:Modifier("public", ?signature).


LocalForwardReflectiveAssignMethodConstantString(?ctx, ?to, ?signature, ?constantHeap) ->
   HeapAllocation(?constantHeap), Method(?signature), Var(?to), Context(?ctx).

LocalForwardReflectiveAssignMethodConstantString(?ctx, ?to, ?signature, ?constantHeap) <-
  ForwardReflectiveAssignMethodConstantString(?ctx, ?to, ?signature, ?mergeHeap),
  _OptVarToMethod[?to] = ?inmethod,
  AssignHeapInMethod(?constantHeap, ?inmethod, ?mergeHeap).
  
// If it is a local and precise string, consider it a high-confidence inference
ForwardHighConfidenceReflectiveAssignMethodConstant(?ctx, ?to, ?signature) <-
  LocalForwardReflectiveAssignMethodConstantString(?ctx, ?to, ?signature, ?constantHeap),
  (NumberOfMethodsMatchingConstant[?constantHeap] < StringPrecisionThreshold[];
   MethodNameMatchingStringConstant:Signature(?signature, ?constantHeap)).

AssignHeapInMethod(?constantHeap, ?inmethod, ?mergeHeap) -> HeapAllocation(?mergeHeap), HeapAllocation(?constantHeap), Method(?inmethod).

AssignHeapInMethod(?constantHeap, ?inmethod, ?constantHeap) <-
   StringConstant(?constantHeap),
   AssignHeapAllocation(?constantHeap, _, ?inmethod),
   !(HeapAllocation:Merge[?constantHeap] = _).

AssignHeapInMethod(?heap, ?inmethod, ?mergeHeap) <-
  StringConstant(?heap),
  AssignHeapAllocation(?heap, _, ?inmethod),
  HeapAllocation:Merge[?heap] = ?mergeHeap.


// TODO: Method objects derived from dynamic info have higher confidence.

/*
//VERSION-A
// REVIEW: why reuse the hctx? Just for lack of anything better? Also later.
ArrayIndexPointsTo(?hctx, ?value, ?hctx, ?arrayValue) <-
  FillArray:Methods(?hctx, ?arrayValue, ?type),
  Method:DeclaringType[?signature] = ?type,
  ReifiedMethod[?signature] = ?value,
  MethodNameMatchingStringConstant:Signature(?signature, _).
*/
FillArray:Methods(?hctx, ?arrayValue, ?type),
VarPointsTo(?hctx, ?arrayValue, ?ctx, ?to) <-
  ImmutableHContextFromContext[?ctx] = ?hctx,
  java:lang:Class:getDeclaredMethods(?invocation, ?to, ?from),
  VarPointsTo(_, ?classValue, ?ctx, ?from),
  _OptReifiedClass(?type, ?classValue),
  _OptReflectiveAllocation(?arrayValue, ?invocation).

// getDeclaredMethod has two differences from getMethod: it retrieves
// all methods, not just public ones, but it does not retrieve inherited
// methods.
ForwardReflectiveAssignMethodConstant(?ctx, ?to, ?signature),
ForwardReflectiveAssignMethodConstantString(?ctx, ?to, ?signature, ?constantHeap) <-
  java:lang:Class:getDeclaredMethod(?to, ?param, ?from),
  VarPointsTo(_, ReifiedClass[?type], ?ctx, ?from),
  VarPointsTo(_, ?constant, ?ctx, ?param),
  Value:Heap[?constant] = ?constantHeap,
  MethodNameMatchingStringConstant:Signature(?signature, ?constantHeap),
  Method:DeclaringType[?signature] = ?type.


/*************************************************************
 * java.lang.Class.getConstructors and variants
 *
 *   public Constructor[] getConstructors()
 *   public Constructor getConstructor(Class[] parameterTypes)
 *   public Constructor[] getDeclaredConstructors()
 *   public Constructor getDeclaredConstructor(Class[] parameterTypes)
 *************************************************************/

/*
//VERSION-A
ArrayIndexPointsTo(?hctx, ?value, ?hctx, ?arrayValue) <-
  FillArray:PublicConstructors(?hctx, ?arrayValue, ?classValue),
  OptClassToConstructor(?constructor, ?classValue),
  Method:Modifier("public", ?constructor),
  ReifiedConstructor[?constructor] = ?value.
*/
FillArray:PublicConstructors(?hctx, ?arrayValue, ?classValue),
VarPointsTo(?hctx, ?arrayValue, ?ctx, ?to) <-
  ImmutableHContextFromContext[?ctx] = ?hctx,
  java:lang:Class:getConstructors(?invocation, ?to, ?from),
  VarPointsTo(_, ?classValue, ?ctx, ?from),
  _OptReflectiveAllocation(?arrayValue, ?invocation).

// NOTE Because of the inherent imprecision in array handling (the argument),
// we assume that any constructor might be returned here.
VarPointsTo(?hctx, ?value, ?ctx, ?to) <-
  ImmutableHContextFromContext[?ctx] = ?hctx,
  java:lang:Class:getConstructor(?to, ?from),
  VarPointsTo(_, ?classValue, ?ctx, ?from),
  OptClassToConstructor(?constructor, ?classValue),
  Method:Modifier("public", ?constructor),
  ReifiedConstructor[?constructor] = ?value.

/*
//VERSION-A
ArrayIndexPointsTo(?hctx, ?value, ?hctx, ?arrayValue) <-
  FillArray:Constructors(?hctx, ?arrayValue, ?classValue),
  OptClassToConstructor(?constructor, ?classValue),
  ReifiedConstructor[?constructor] = ?value.
*/
FillArray:Constructors(?hctx, ?arrayValue, ?classValue),
VarPointsTo(?hctx, ?arrayValue, ?ctx, ?to) <-
  ImmutableHContextFromContext[?ctx] = ?hctx,
  java:lang:Class:getDeclaredConstructors(?invocation, ?to, ?from),
  VarPointsTo(_, ?classValue, ?ctx, ?from),
  _OptReflectiveAllocation(?arrayValue, ?invocation).


// NOTE Because of the inherent imprecision in array handling (the argument),
// we assume that any constructor might be returned here.
VarPointsTo(?hctx, ?value, ?ctx, ?to) <-
  ImmutableHContextFromContext[?ctx] = ?hctx,
  java:lang:Class:getDeclaredConstructor(?to, ?from),
  VarPointsTo(_, ?classValue, ?ctx, ?from),
  OptClassToConstructor(?constructor, ?classValue),
  ReifiedConstructor[?constructor] = ?value.


/*************************************************************
 * java.lang.Class.getFields and variants
 *
 *   public Field[] getFields()
 *   public Field getField(String name)
 *   public Field[] getDeclaredFields()
 *   public Field getDeclaredField(String name)
 *
 * NOTE
 * If this Class object represents a class, getFields returns the public fields
 * of this class and of all its superclasses. If this Class object represents
 * an interface, getFields returns the fields of this interface and of all its
 * superinterfaces. The same apply to getField.
 *
 * getDeclaredFields and getDeclaredField will only search in the fields
 * directly declared in the class or interface.
 *************************************************************/

/*
//VERSION-A
ArrayIndexPointsTo(?hctx, ?value, ?hctx, ?arrayValue) <-
  FillArray:PublicFields(?hctx, ?arrayValue, ?type),
  SupertypeOf(?superClass, ?type),
  Field:DeclaringType[?fld] = ?superClass,
  Field:Modifier("public", ?fld),
  ReifiedField[?fld] = ?value,
  FieldNameMatchingStringConstant:Signature(?fld, _).
*/
FillArray:PublicFields(?hctx, ?arrayValue, ?type),
VarPointsTo(?hctx, ?arrayValue, ?ctx, ?to) <-
  ImmutableHContextFromContext[?ctx] = ?hctx,
  java:lang:Class:getFields(?invocation, ?to, ?from),
  VarPointsTo(_, ?classValue, ?ctx, ?from),
  _OptReifiedClass(?type, ?classValue),
  _OptReflectiveAllocation(?arrayValue, ?invocation).

// NOTE that this won't fire when we are in DISTINGUISH_NO_STRING_CONSTANTS
// mode. The constant will never flow to ?param.
ForwardReflectiveAssignFieldConstant(?ctx, ?to, ?signature),
ForwardReflectiveAssignFieldConstantString(?ctx, ?to, ?signature, ?constantHeap) <-
  java:lang:Class:getField(?to, ?param, ?from),
  VarPointsTo(_, ReifiedClass[?type], ?ctx, ?from),
  VarPointsTo(_, ?constant, ?ctx, ?param),
  Value:Heap[?constant] = ?constantHeap,
  FieldNameMatchingStringConstant:Signature(?signature, ?constantHeap),
  SupertypeOf(?superClass, ?type),
  Field:DeclaringType[?signature] = ?superClass,
  Field:Modifier("public", ?signature).

// If it is a local and precise string, consider it a high-confidence inference
ForwardHighConfidenceReflectiveAssignFieldConstant(?ctx, ?to, ?signature) <-
  ForwardReflectiveAssignFieldConstantString(?ctx, ?to, ?signature, ?mergeHeap),
  _OptVarToMethod[?to] = ?inmethod,
  AssignHeapInMethod(?constantHeap, ?inmethod, ?mergeHeap),
  (NumberOfFieldsMatchingConstant[?constantHeap] < StringPrecisionThreshold[];
   FieldNameMatchingStringConstant:Signature(?signature, ?constantHeap)).

/*
//VERSION-A
ArrayIndexPointsTo(?hctx, ?value, ?hctx, ?arrayValue) <-
  FillArray:Fields(?hctx, ?arrayValue, ?class),
  Field:DeclaringType[?fld] = ?class,
  ReifiedField[?fld] = ?value,
  FieldNameMatchingStringConstant:Signature(?fld, _).
*/
FillArray:Fields(?hctx, ?arrayValue, ?type),
VarPointsTo(?hctx, ?arrayValue, ?ctx, ?to) <-
  ImmutableHContextFromContext[?ctx] = ?hctx,
  java:lang:Class:getDeclaredFields(?invocation, ?to, ?from),
  VarPointsTo(_, ?classValue, ?ctx, ?from),
  _OptReifiedClass(?type, ?classValue),
  _OptReflectiveAllocation(?arrayValue, ?invocation).

ForwardReflectiveAssignFieldConstant(?ctx, ?to, ?signature),
ForwardReflectiveAssignFieldConstantString(?ctx, ?to, ?signature, ?constantHeap) <-
  java:lang:Class:getDeclaredField(?to, ?param, ?from),
  VarPointsTo(_, ReifiedClass[?type], ?ctx, ?from),
  VarPointsTo(_, ?constant, ?ctx, ?param),
  Value:Heap[?constant] = ?constantHeap,
  FieldNameMatchingStringConstant:Signature(?signature, ?constantHeap),
  Field:DeclaringType[?signature] = ?type.


/*************************************************************
 * java.lang.Class.newInstance
 *
 *   public Object newInstance() 
 *
 * The class is instantiated as if by a new expression with an empty argument
 * list. The class is initialized if it has not already been initialized.
 * Throws:
 *   - IllegalAccessException if the class or its nullary constructor is not accessible
 *   - InstantiationException if this Class represents an abstract class, an
 *   interface, an array class, a primitive type, or void; or if the class has
 *   no nullary constructor.
 *
 * NOTE
 *    - For the OpenJDK there is a duplication of simulating, since we
 *      also simulate reflective constructor invocations. We'll soon
 *      sort out what needs to be in #ifdefs exactly.
 *************************************************************/

// Simple optimization predicate. It matters.
OptClassToConstructor(?constructor, ?classValue) <-
  ReifiedClass[?type] = ?classValue,
  Method:DeclaringType[?constructor] = ?type,
  Method:SimpleName[?constructor] = "<init>".

AssignReflectiveAllocation(?invocation, ?type, ?ctx, ?to),
ReflectiveSpecialMethodInvocation(?invocation, ?constructor, ?ctx, ?to),
ReflectiveBaseVar[?invocation] = ?to <-
  java:lang:Class:newInstance(?invocation, ?to, ?from),
  VarPointsTo(_, ?class, ?ctx, ?from),
  OptClassToConstructor(?constructor, ?class),
  Method:DeclaringType[?constructor] = ?type.


/*************************************************************
 * java.lang.Class.getClassLoader
 *
 *   public ClassLoader getClassLoader()
 *
 * NOTE  
 * We just create a dummy value to represent the ClassLoader returned.
 *************************************************************/
_OptVarToMethod[?var] = ?method <-
  Var:DeclaringMethod(?var, ?method).
  
_Optjava:lang:Class:getClassLoader(?invocation, ?to, ?method) <-
  java:lang:Class:getClassLoader(?invocation, ?to, ?from),
  _OptVarToMethod[?from] = ?method.

VarPointsTo(?hctx, ?value, ?ctx, ?to) <-
  ImmutableHContextFromContext[?ctx] = ?hctx,
  _Optjava:lang:Class:getClassLoader(?invocation, ?to, ?method),
  ReachableContext(?ctx, ?method),
  Type:Id(?type:"java.lang.ClassLoader"),
  ReflectiveAllocation[?invocation, ?type] = ?value.
  
/*************************************************************
 * java.lang.ClassLoader.getResource
 *
 *   public URL getResource(java.lang.String)
 *
 * NOTE  
 * We just create a dummy value to represent the URL resource returned.
 *************************************************************/
VarPointsTo(?hctx, ?value, ?ctx, ?to) <-
  ImmutableHContextFromContext[?ctx] = ?hctx,
  ReachableGetResource(?to, ?ctx, ?invocation),
  UrlReflectiveAllocation[?invocation] = ?value.

ReachableGetResource(?to, ?ctx, ?invocation) <-
  java:lang:ClassLoader:getResource(?invocation, ?to, ?from),
  Var:DeclaringMethod(?from, ?method),
  ReachableContext(?ctx, ?method).

UrlReflectiveAllocation[?invocation] = ?value <-
  Type:Id(?type:"java.net.URL"),
  ReflectiveAllocation[?invocation, ?type] = ?value.

/*************************************************************
 * Auxiliaries
 *************************************************************/
NumberOfClassesMatchingConstant[?constant] = ?n <-
  agg<<?n = count()>>(ClassNameMatchingStringConstant:Type(_, ?constant)).

//NumberOfMethodsMatchingMergedConstant[?constant] = ?n <-
//   agg<<?n = count()>>(MethodNameMatchingStringConstant:Signature(_, ?constant)).
NumberOfMethodsMatchingConstant[?constant] = ?n <-
   agg<<?n = count()>>(PreMethodNameMatchingStringConstant:Signature(_, ?constant)).

                                             
//NumberOfFieldsMatchingMergedConstant[?constant] = ?n <-
//  agg<<?n = count()>>(FieldNameMatchingStringConstant:Signature(_, ?constant)).
NumberOfFieldsMatchingConstant[?constant] = ?n <-
  agg<<?n = count()>>(PreFieldNameMatchingStringConstant:Signature(_, ?constant)).

StringPrecisionThreshold[] = 10.
