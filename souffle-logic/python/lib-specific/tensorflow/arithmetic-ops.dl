//----------------------------------------------------------------------------------------------------------------------
//                                          OPERATIONS THAT ARE BASED ON BROADCASTING
//                                              tf.add,+,-,*,tf.multiply tf.add_n
//----------------------------------------------------------------------------------------------------------------------
.decl BroadcastingFunctionInfo(?function:Function, ?funName:symbol)
.output BroadcastingFunctionInfo

BroadcastingFunctionInfo("<tensorflow.multiply>", "multiply").
BroadcastingFunctionInfo("<tensorflow.add>", "add").


.decl TfBroadcastingOp(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?broadcastingOp:Value, ?offset:number)
.output TfBroadcastingOp

.decl TfBroadcastingOpCannotBroadcast(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?broadcastingOp:Value)
.output TfBroadcastingOpCannotBroadcast

.decl TfBroadcastingOpBroadcastWarning(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?broadcastingOp:Value)
.output TfBroadcastingOpBroadcastWarning

.decl TfBroadcastingOpAllDimsHaveRes(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?broadcastingOp:Value)
.output TfBroadcastingOpAllDimsHaveRes

.decl TfBroadcastingOpAllDimsUpToXHaveRes(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?broadcastingOp:Value, ?index:number)
.output TfBroadcastingOpAllDimsUpToXHaveRes

.decl TfBroadcastingOpResultShapeListLength(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?broadcastingOp:Value, ?len:number)
.output TfBroadcastingOpResultShapeListLength

.decl TfBroadcastingOpResultShapeListContent(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?broadcastingOp:Value, ?index:number, ?indexVal:Value)
.output TfBroadcastingOpResultShapeListContent


TfBroadcastingOpResultShapeListLength(?ctx, ?insn, ?broadcastingOp, ?len1),
TensorOperation_NameToVal(?ctx, ?insn, ?broadcastingOp, "op1", ?tensor1Hctx, ?tensor1),
TensorOperation_NameToVal(?ctx, ?insn, ?broadcastingOp, "op2", ?tensor2Hctx, ?tensor2),
TfBroadcastingOp(?ctx, ?insn, ?broadcastingOp, ?offset):-
  BroadcastingFunctionInfo(?toFunction, ?funName),
  ResolvedActualParamValue(?ctx, ?insn, ?toFunction, "x", ?tensor1Hctx, ?tensor1),
  ResolvedActualParamValue(?ctx, ?insn, ?toFunction, "y", ?tensor2Hctx, ?tensor2),
  TensorLikeObjectShapeListLength(?tensor1Hctx, ?tensor1, ?len1),
  TensorLikeObjectShapeListLength(?tensor2Hctx, ?tensor2, ?len2),
  ?len1 >= ?len2,
  ?offset = ?len1 - ?len2,
  Instruction_Function(?insn, ?fun),
  Instruction_Index(?insn, ?index),
  ?broadcastingOp = PlaceHolderValueMacro(?funName, ?fun, ?index, ?tensor1, ?tensor2).

TfBroadcastingOpResultShapeListLength(?ctx, ?insn, ?broadcastingOp, ?len2),
TensorOperation_NameToVal(?ctx, ?insn, ?broadcastingOp, "op1", ?tensor2Hctx, ?tensor2),
TensorOperation_NameToVal(?ctx, ?insn, ?broadcastingOp, "op2", ?tensor1Hctx, ?tensor1),
TfBroadcastingOp(?ctx, ?insn, ?broadcastingOp, ?offset):-
  BroadcastingFunctionInfo(?toFunction, ?funName),
  ResolvedActualParamValue(?ctx, ?insn, ?toFunction, "x", ?tensor1Hctx, ?tensor1),
  ResolvedActualParamValue(?ctx, ?insn, ?toFunction, "y", ?tensor2Hctx, ?tensor2),
  TensorLikeObjectShapeListLength(?tensor1Hctx, ?tensor1, ?len1),
  TensorLikeObjectShapeListLength(?tensor2Hctx, ?tensor2, ?len2),
  ?len2 > ?len1,
  ?offset = ?len2 - ?len1,
  Instruction_Function(?insn, ?fun),
  Instruction_Index(?insn, ?index),
  ?broadcastingOp = PlaceHolderValueMacro(?funName, ?fun, ?index, ?tensor1, ?tensor2).


TfBroadcastingOpResultShapeListLength(?ctx, ?insn, ?broadcastingOp, ?len1),
TensorOperation_NameToVal(?ctx, ?insn, ?broadcastingOp, "op1", ?tensor1Hctx, ?tensor1),
TensorOperation_NameToVal(?ctx, ?insn, ?broadcastingOp, "op2", ?tensor2Hctx, ?tensor2),
TfBroadcastingOp(?ctx, ?insn, ?broadcastingOp, ?offset):-
  isAssignBinop_Insn(?insn),
  AssignOper_From(?insn, 1, ?from1),
  AssignOper_From(?insn, 2, ?from2),
  Assign_Operation(?insn, ?op),
  (?op = "add" ; ?op = "rem"; ?op = "mul"),
  VarPointsTo(?tensor1Hctx, ?tensor1, ?ctx, ?from1),
  VarPointsTo(?tensor2Hctx, ?tensor2, ?ctx, ?from2),
  TensorLikeObjectShapeListLength(?tensor1Hctx, ?tensor1, ?len1),
  TensorLikeObjectShapeListLength(?tensor2Hctx, ?tensor2, ?len2),
  ?len1 >= ?len2,
  ?offset = ?len1 - ?len2,
  Instruction_Function(?insn, ?fun),
  Instruction_Index(?insn, ?index),
  ?broadcastingOp = PlaceHolderValueMacro(?op, ?fun, ?index, ?tensor1, ?tensor2).

TfBroadcastingOpResultShapeListLength(?ctx, ?insn, ?broadcastingOp, ?len2),
TensorOperation_NameToVal(?ctx, ?insn, ?broadcastingOp, "op1", ?tensor2Hctx, ?tensor2),
TensorOperation_NameToVal(?ctx, ?insn, ?broadcastingOp, "op2", ?tensor1Hctx, ?tensor1),
TfBroadcastingOp(?ctx, ?insn, ?broadcastingOp, ?offset):-
  isAssignBinop_Insn(?insn),
  AssignOper_From(?insn, 1, ?from1),
  AssignOper_From(?insn, 2, ?from2),
  Assign_Operation(?insn, ?op),
  (?op = "add" ; ?op = "rem"; ?op = "mul"),
  VarPointsTo(?tensor1Hctx, ?tensor1, ?ctx, ?from1),
  VarPointsTo(?tensor2Hctx, ?tensor2, ?ctx, ?from2),
  TensorLikeObjectShapeListLength(?tensor1Hctx, ?tensor1, ?len1),
  TensorLikeObjectShapeListLength(?tensor2Hctx, ?tensor2, ?len2),
  ?len2 > ?len1,
  ?offset = ?len2 - ?len1,
  Instruction_Function(?insn, ?fun),
  Instruction_Index(?insn, ?index),
  ?broadcastingOp = PlaceHolderValueMacro(?op, ?fun, ?index, ?tensor1, ?tensor2).

TfBroadcastingOpResultShapeListContent(?ctx, ?insn, ?broadcastingOp, ?index, ?indexVal):-
  TfBroadcastingOp(?ctx, ?insn, ?broadcastingOp, ?offset),
  ?index < ?offset,
  TensorOperation_NameToVal(?ctx, ?insn, ?broadcastingOp, "op1", ?tensor1Hctx, ?tensor1),
  TensorLikeObjectShapeListContent(?tensor1Hctx, ?tensor1, ?index, ?indexVal).

TfBroadcastingOpResultShapeListContent(?ctx, ?insn, ?broadcastingOp, ?index, ?indexVal):-
  TfBroadcastingOp(?ctx, ?insn, ?broadcastingOp, ?offset),
  ?index >= ?offset,
  TensorOperation_NameToVal(?ctx, ?insn, ?broadcastingOp, "op1", ?tensor1Hctx, ?tensor1),
  TensorOperation_NameToVal(?ctx, ?insn, ?broadcastingOp, "op2", ?tensor2Hctx, ?tensor2),
  TensorLikeObjectShapeListContent(?tensor1Hctx, ?tensor1, ?index, ?indexVal),
  TensorLikeObjectShapeListContent(?tensor2Hctx, ?tensor2, ?index - ?offset, ?indexVal).

TfBroadcastingOpBroadcastWarning(?ctx, ?insn, ?broadcastingOp),
TfBroadcastingOpResultShapeListContent(?ctx, ?insn, ?broadcastingOp, ?index, ?indexVal):-
  TfBroadcastingOp(?ctx, ?insn, ?broadcastingOp, ?offset),
  ?index >= ?offset,
  TensorOperation_NameToVal(?ctx, ?insn, ?broadcastingOp, "op1", ?tensor1Hctx, ?tensor1),
  TensorOperation_NameToVal(?ctx, ?insn, ?broadcastingOp, "op2", ?tensor2Hctx, ?tensor2),
  TensorLikeObjectShapeListContent(?tensor1Hctx, ?tensor1, ?index, ?indexVal1),
  TensorLikeObjectShapeListContent(?tensor2Hctx, ?tensor2, ?index - ?offset, ?indexVal2),
  ?indexVal1 != ?indexVal2,
  (
  (?indexVal1 = "<num-constant(int):1>", ?indexVal = ?indexVal2)
  ;
  (?indexVal2 = "<num-constant(int):1>", ?indexVal = ?indexVal2)
  ).

TfBroadcastingOpCannotBroadcast(?ctx, ?insn, ?broadcastingOp):-
  TfBroadcastingOp(?ctx, ?insn, ?broadcastingOp, ?offset),
  ?index >= ?offset,
  TensorOperation_NameToVal(?ctx, ?insn, ?broadcastingOp, "op1", ?tensor1Hctx, ?tensor1),
  TensorOperation_NameToVal(?ctx, ?insn, ?broadcastingOp, "op2", ?tensor2Hctx, ?tensor2),
  TensorLikeObjectShapeListContent(?tensor1Hctx, ?tensor1, ?index, ?indexVal1),
  TensorLikeObjectShapeListContent(?tensor2Hctx, ?tensor2, ?index - ?offset, ?indexVal2),
  ?indexVal1 != ?indexVal2,
  ?indexVal1 != "<num-constant(int):1>",
  ?indexVal2 != "<num-constant(int):1>".

TfBroadcastingOpAllDimsUpToXHaveRes(?ctx, ?insn, ?broadcastingOp, 0):-
  TfBroadcastingOpResultShapeListContent(?ctx, ?insn, ?broadcastingOp, 0, _).

TfBroadcastingOpAllDimsUpToXHaveRes(?ctx, ?insn, ?broadcastingOp, ?index):-
  ?index > 0,
  TfBroadcastingOpResultShapeListContent(?ctx, ?insn, ?broadcastingOp, ?index, _),
  TfBroadcastingOpAllDimsUpToXHaveRes(?ctx, ?insn, ?broadcastingOp, ?index - 1).

TensorOperationProducesOutput(?ctx, ?insn, ?broadcastingOp):-
  TfBroadcastingOpAllDimsHaveRes(?ctx, ?insn, ?broadcastingOp).

TfBroadcastingOpAllDimsHaveRes(?ctx, ?insn, ?broadcastingOp):-
  TfBroadcastingOpResultShapeListLength(?ctx, ?insn, ?broadcastingOp, ?len),
  TfBroadcastingOpAllDimsUpToXHaveRes(?ctx, ?insn, ?broadcastingOp, ?len-1).

TensorShapeListLength(?shapeHctx, ?tensorShapeVal, ?len):-
  TfBroadcastingOpResultShapeListLength(?ctx, ?insn, ?broadcastingOp, ?len),
  TensorOperationProducesOutput(?ctx, ?insn, ?broadcastingOp),
  AssignInstruction_To(?insn, ?var),
  VarPointsTo(?hctx, ?broadcastingOp, ?ctx, ?var),
  TensorShape(?hctx, ?broadcastingOp, ?shapeHctx, ?tensorShapeVal).

TensorShapeListContent(?shapeHctx, ?tensorShapeVal, ?index, ?indexVal):-
  TfBroadcastingOp(?ctx, ?insn, ?broadcastingOp, _),
  TensorOperationProducesOutput(?ctx, ?insn, ?broadcastingOp),
  AssignInstruction_To(?insn, ?var),
  VarPointsTo(?hctx, ?broadcastingOp, ?ctx, ?var),
  TensorShape(?hctx, ?broadcastingOp, ?shapeHctx, ?tensorShapeVal),
  TfBroadcastingOpResultShapeListContent(?ctx, ?insn, ?broadcastingOp, ?index, ?indexVal).

//----------------------------------------------------------------------------------------------------------------------
//                                              MATMUL
//----------------------------------------------------------------------------------------------------------------------
.decl TfMatMulOp(?ctx:configuration.Context, ?insn:FunctionInvocation_Insn, ?matMulOp:Value)
.output TfMatMulOp



//----------------------------------------------------------------------------------------------------------------------
//                                               TENSORDOT
//----------------------------------------------------------------------------------------------------------------------

