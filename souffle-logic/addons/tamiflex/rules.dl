LoadArrayIndex(?array, ?value, ?inmethod) :-
  ?sig = "<java.lang.reflect.Array: java.lang.Object get(java.lang.Object,int)>",
  isMethod(?sig),
  StaticMethodInvocation(?invocation, ?sig, ?inmethod),
  ActualParam(0, ?invocation, ?array),
  AssignReturnValue(?invocation, ?value).

StoreArrayIndex(?value, ?array, ?inmethod) :-
  ?sig = "<java.lang.reflect.Array: void set(java.lang.Object,int,java.lang.Object)>",
  isMethod(?sig),
  StaticMethodInvocation(?invocation, ?sig, ?inmethod),
  ActualParam(0, ?invocation, ?array),
  ActualParam(2, ?invocation, ?value).

LoadInstanceField(?base, ?fld, ?to, ?inmethod) :-
  ?sig = "<java.lang.reflect.Field: java.lang.Object get(java.lang.Object)>",
  isMethod(?sig),
  VirtualMethodInvocation(?invocation, ?sig, ?inmethod),
  Tamiflex_ReflectionMap(?fld, ?invocation),
  isField(?fld),
  ! Field_Modifier("static", ?fld),
  ActualParam(0, ?invocation, ?base),
  AssignReturnValue(?invocation, ?to).

LoadStaticField(?fld, ?to, ?inmethod) :-
  ?sig = "<java.lang.reflect.Field: java.lang.Object get(java.lang.Object)>",
  isMethod(?sig),
  VirtualMethodInvocation(?invocation, ?sig, ?inmethod),
  Tamiflex_ReflectionMap(?fld, ?invocation),
  isField(?fld),
  Field_Modifier("static", ?fld),
  AssignReturnValue(?invocation, ?to).

StoreInstanceField(?from, ?base, ?fld, ?inmethod) :-
  ?sig = "<java.lang.reflect.Field: void set(java.lang.Object,java.lang.Object)>",
  isMethod(?sig),
  VirtualMethodInvocation(?invocation, ?sig, ?inmethod),
  Tamiflex_ReflectionMap(?fld, ?invocation),
  isField(?fld),
  ! Field_Modifier("static", ?fld),
  ActualParam(0, ?invocation, ?base),
  ActualParam(1, ?invocation, ?from).

StoreStaticField(?from, ?fld, ?inmethod) :-
  ?sig = "<java.lang.reflect.Field: void set(java.lang.Object,java.lang.Object)>",
  isMethod(?sig),
  VirtualMethodInvocation(?invocation, ?sig, ?inmethod),
  Tamiflex_ReflectionMap(?fld, ?invocation),
  isField(?fld),
  Field_Modifier("static", ?fld),
  ActualParam(1, ?invocation, ?from).

Value_DeclaringType(?value, ?type) :-
  (Tamiflex_Class_NewInstance(?invocation, ?value) ;
   Tamiflex_Constructor_NewInstance(?invocation, _, ?value) ;
   Tamiflex_Array_NewInstance(?invocation, ?value)),
  Instruction_Method(?invocation, ?meth),
  Method_DeclaringType(?meth, ?type).

Value_DeclaringType(?value, ?type) :-
  (Tamiflex_ReifiedField(_, ?value, _) ;
   Tamiflex_ReifiedFieldArray(_, ?value, _) ;
   Tamiflex_ReifiedMethod(_, ?value, _) ;
   Tamiflex_ReifiedMethodArray(_, ?value, _)),
  Value_Type(?value, ?type).

MockValueConsMacro(cat(cat(cat(cat("<(Tamiflex) ", ?invocation), "/"), ?type), ">"), ?type),
Tamiflex_Class_NewInstance(?invocation, cat(cat(cat(cat("<(Tamiflex) ", ?invocation), "/"), ?type), ">")) :-
  ?sig = "<java.lang.Class: java.lang.Object newInstance()>",
  isMethod(?sig),
  VirtualMethodInvocation(?invocation, ?sig, _),
  Tamiflex_ReflectionMap(?type, ?invocation),
  isInstruction(?invocation),
  isType(?type).

MockValueConsMacro(cat(cat(cat(cat("<(Tamiflex) ", ?invocation), "/"), ?type), ">"), ?type),
Tamiflex_Constructor_NewInstance(?invocation, ?constructor, cat(cat(cat(cat("<(Tamiflex) ", ?invocation), "/"), ?type), ">")) :-
  ?sig = "<java.lang.reflect.Constructor: java.lang.Object newInstance(java.lang.Object[])>",
  isMethod(?sig),
  VirtualMethodInvocation(?invocation, ?sig, _),
  Tamiflex_ReflectionMap(?constructor, ?invocation),
  isInstruction(?invocation),
  isMethod(?constructor),
  Method_DeclaringType(?constructor, ?type),
  isType(?type).

MockValueConsMacro(cat(cat(cat(cat("<(Tamiflex) ", ?invocation), "/"), ?type), ">"), ?type),
Tamiflex_Array_NewInstance(?invocation, cat(cat(cat(cat("<(Tamiflex) ", ?invocation), "/"), ?type), ">")) :-
  ?sig = "<java.lang.reflect.Array: java.lang.Object newInstance(java.lang.Class,int)>",
  isMethod(?sig),
  StaticMethodInvocation(?invocation, ?sig, _),
  Tamiflex_ReflectionMap(?type, ?invocation),
  isInstruction(?invocation),
  isType(?type).

MockValueConsMacro(cat(cat("<(Tamiflex) ", ?fld), ">"), ?type),
Tamiflex_ReifiedField(?fld, cat(cat("<(Tamiflex) ", ?fld), ">"), ?invocation) :-
  (?sig = "<java.lang.Class: java.lang.reflect.Field getDeclaredField(java.lang.String)>" ;
   ?sig = "<java.lang.Class: java.lang.reflect.Field getField(java.lang.String)>" ),
  isMethod(?sig),
  VirtualMethodInvocation(?invocation, ?sig, _),
  Tamiflex_ReflectionMap(?fld, ?invocation),
  isField(?fld),
  ?type = "java.lang.reflect.Field",
  isType(?type).

MockValueConsMacro(cat(cat("<(Tamiflex) ", ?type), "/Field[]>"), ?javaFieldArrayType),
Tamiflex_ReifiedFieldArray(?type, cat(cat("<(Tamiflex) ", ?type), "/Field[]>"), ?invocation) :-
  (?sig = "<java.lang.Class: java.lang.reflect.Field[] getDeclaredFields()>" ;
   ?sig = "<java.lang.Class: java.lang.reflect.Field[] getFields()>" ),
  isMethod(?sig),
  VirtualMethodInvocation(?invocation, ?sig, _),
  Tamiflex_ReflectionMap(?type, ?invocation),
  isType(?type),
  ?javaFieldArrayType = "java.lang.reflect.Field[]",
  isType(?javaFieldArrayType).


MockValueConsMacro(cat(cat("<(Tamiflex) ", ?method), ">"), ?type),
Tamiflex_ReifiedMethod(?method, cat(cat("<(Tamiflex) ", ?method), ">"), ?invocation) :-
  (?sig = "<java.lang.Class: java.lang.reflect.Method getDeclaredMethod(java.lang.String,java.lang.Class[])>" ;
   ?sig = "<java.lang.Class: java.lang.reflect.Method getMethod(java.lang.String,java.lang.Class[])>" ),
  isMethod(?sig),
  VirtualMethodInvocation(?invocation, ?sig, _),
  Tamiflex_ReflectionMap(?method, ?invocation),
  isMethod(?method),
  ?type = "java.lang.reflect.Method",
  isType(?type).

MockValueConsMacro(cat(cat("<(Tamiflex) ", ?type), "/Method[]>"), ?javaMethodArrayType),
Tamiflex_ReifiedMethodArray(?type, cat(cat("<(Tamiflex) ", ?type), "/Method[]>"), ?invocation) :-
  (?sig = "<java.lang.Class: java.lang.reflect.Method[] getDeclaredMethods()>" ;
   ?sig = "<java.lang.Class: java.lang.reflect.Method[] getMethods()>" ),
  isMethod(?sig),
  VirtualMethodInvocation(?invocation, ?sig, _),
  Tamiflex_ReflectionMap(?type, ?invocation),
  isType(?type),
  ?javaMethodArrayType = "java.lang.reflect.Method[]",
  isType(?javaMethodArrayType).


/**
 * The analysis is done under a default context (ImmutableContext). No
 * reason to use normal context for reflective calls, since we are
 * using the results from tamiflex to resolve targets. Thus there is
 * no information to distinguish things per context.
 */

/**
 * A tamiflex call graph edge is not a real one: types in the signature
 * are not preserved (e.g., arguments are passed as an array). This is
 * much like reflective call graph edges.
 */
.decl TamiflexCallGraphEdge(?callerCtx:configuration.Context, ?invocation:MethodInvocation,
                            ?calleeCtx:configuration.Context, ?callee:Method)

ReachableContext(?calleeCtx, ?callee) :-
  TamiflexCallGraphEdge(_, _, ?calleeCtx, ?callee).

.decl OptForNameInvocation(?invocation:MethodInvocation, ?inmethod:Method)

OptForNameInvocation(?invocation, ?inmethod) :-
  (?sig = "<java.lang.Class: java.lang.Class forName(java.lang.String)>" ;
   ?sig = "<java.lang.Class: java.lang.Class forName(java.lang.String,boolean,java.lang.ClassLoader)>" ),
  isMethod(?sig),
  StaticMethodInvocation(?invocation, ?sig, ?inmethod).

VarPointsTo(?hctx, ?classObj, ?ctx, ?return) :-
  OptForNameInvocation(?invocation, ?inmethod),
  ReachableContext(?ctx, ?inmethod),
  isImmutableHContext(?hctx),
  Tamiflex_ReflectionMap(?class, ?invocation),
  ?classObj = cat(cat("<class ", ?class), ">"),
  isHeapAllocation(?classObj),
  HeapAllocation_Type(?classObj, "java.lang.Class"),
  AssignReturnValue(?invocation, ?return).

VarPointsTo(?hctx, ?value, ?ctx, ?return) :-
  (Tamiflex_Class_NewInstance(?invocation, ?value) ;
   Tamiflex_Array_NewInstance(?invocation, ?value) ),
  Instruction_Method(?invocation, ?inmethod),
  AssignReturnValue(?invocation, ?return),
  ReachableContext(?ctx, ?inmethod),
  isImmutableHContext(?hctx).

.decl TamiflexConstructorCallGraphEdge(?callerCtx:configuration.Context, ?invocation:MethodInvocation,
                                       ?calleeCtx:configuration.Context, ?callee:Method)

// MergeMacro(?callerCtx, ?invocation, ?hctx, ?obj, ?calleeCtx),
VarPointsTo(?hctx, ?value, ?callerCtx, ?return),
TamiflexCallGraphEdge(?callerCtx, ?invocation, ?calleeCtx, ?constructor),
TamiflexConstructorCallGraphEdge(?callerCtx, ?invocation, ?calleeCtx, ?constructor),
VarPointsTo(?hctx, ?value, ?calleeCtx, ?this) :-
  Tamiflex_Constructor_NewInstance(?invocation, ?constructor, ?value),
  Instruction_Method(?invocation, ?inmethod),
  ReachableContext(?callerCtx, ?inmethod),
  AssignReturnValue(?invocation, ?return),
  ThisVar(?constructor, ?this),
  isImmutableHContext(?hctx),
  isImmutableContext(?calleeCtx).

Tamiflex_Invoke(?callerCtx, ?invocation, ?method) :-
  ?sig = "<java.lang.reflect.Method: java.lang.Object invoke(java.lang.Object,java.lang.Object[])>",
  isMethod(?sig),
  VirtualMethodInvocation(?invocation, ?sig, _),
  Tamiflex_ReflectionMap(?method, ?invocation),
  isMethod(?method),
  Instruction_Method(?invocation, ?inmethod),
  ReachableContext(?callerCtx, ?inmethod).

/**
 * Private method invocations are special methods, unless they are static.
 * But they are handled the same as virtual methods in the context of tamiflex
 * since tamiflex has already resolved the final target method.
 */
TamiflexCallGraphEdge(?callerCtx, ?invocation, ?calleeCtx, ?callee),
VarPointsTo(?hctx, ?value, ?calleeCtx, ?this) :-
  Tamiflex_Invoke(?callerCtx, ?invocation, ?callee),
  ! Method_Modifier("static", ?callee),
  ActualParam(0, ?invocation, ?base),
  VarPointsTo(?hctx, ?value, ?callerCtx, ?base),
  ThisVar(?callee, ?this),
  isImmutableContext(?calleeCtx).

/**
 * Static method invocations
 */
TamiflexCallGraphEdge(?callerCtx, ?invocation, ?calleeCtx, ?callee) :-
   Tamiflex_Invoke(?callerCtx, ?invocation, ?callee),
   Method_Modifier("static", ?callee),
   isImmutableContext(?calleeCtx).

/**
 * Return values of reflective method invocations
 */
OptInterproceduralAssign(?callerCtx, ?local, ?calleeCtx, ?return) :-
  Tamiflex_Invoke(?callerCtx, ?invocation, ?callee),
  AssignReturnValue(?invocation, ?local),
  ReturnVar(?return, ?callee),
  isImmutableContext(?calleeCtx).

/**
 * Handle the array of actual parameters of a reflective method call
 */

// Optimizing indexing
/// two cases of tamiflex calls: regular reflective invokes ...
.decl TamiflexInvocationFirstParam(?calleeCtx:configuration.Context, ?formal:Var,
                                   ?callerCtx:configuration.Context, ?argsParam:Var)

TamiflexInvocationFirstParam(?calleeCtx, ?formal, ?callerCtx, ?argsParam) :-
  Tamiflex_Invoke(?callerCtx, ?invocation, ?callee),
  ActualParam(1, ?invocation, ?argsParam),
  FormalParam(_, ?callee, ?formal),
  isImmutableContext(?calleeCtx).
/// ... vs. constructor calls
TamiflexInvocationFirstParam(?calleeCtx, ?formal, ?callerCtx, ?argsParam) :-
  TamiflexConstructorCallGraphEdge(?callerCtx, ?invocation, ?calleeCtx, ?callee),
  ActualParam(0, ?invocation, ?argsParam),
  FormalParam(_, ?callee, ?formal).

// Optimizing indexing
.decl TamiflexInvocationArgumentArrayValue(?calleeCtx:configuration.Context, ?formal:Var,
                                           ?basehctx:configuration.HContext, ?basevalue:Value)

TamiflexInvocationArgumentArrayValue(?calleeCtx, ?formal, ?basehctx, ?basevalue) :-
  TamiflexInvocationFirstParam(?calleeCtx, ?formal, ?ctx, ?base),
  VarPointsTo(?basehctx, ?basevalue, ?ctx, ?base).

VarPointsTo(?hctx, ?value, ?calleeCtx, ?formal) :-
  TamiflexInvocationArgumentArrayValue(?calleeCtx, ?formal, ?basehctx, ?basevalue),
  ArrayIndexPointsTo(?hctx, ?value, ?basehctx, ?basevalue),
  Var_Type(?formal, ?type),
  Value_Type(?value, ?valuetype),
  basic.SupertypeOf(?type, ?valuetype).

VarPointsTo(?hctx, ?value, ?ctx, ?return) :-
  (Tamiflex_ReifiedField(_, ?value, ?invocation) ;
   Tamiflex_ReifiedFieldArray(_, ?value, ?invocation) ;
   Tamiflex_ReifiedMethod(_, ?value, ?invocation) ;
   Tamiflex_ReifiedMethodArray(_, ?value, ?invocation) ),
  Instruction_Method(?invocation, ?inmethod),
  ReachableContext(?ctx, ?inmethod),
  isImmutableHContext(?hctx),
  AssignReturnValue(?invocation, ?return).
